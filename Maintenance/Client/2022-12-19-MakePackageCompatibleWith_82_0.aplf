  {data}←MakePackageCompatibleWith_82_0∆2022_12_19 data;tags;groupName
⍝ Performs actions that make any packages fulfill the requirements of version 0.81.0:
⍝  * Change the name of "info_url" to "project_url"
⍝  * Introduce the three properties "os_lin", "os_mac" and "os_win" and make an effort
⍝    in order to get their values from tags if they are really new
⍝  * Introduce the property "minimumAplVersion"
⍝  * Delete the now deprecated property "wx"
⍝  * Polish "tags" so that they are in line with Tatin's policies

 groupName←data.group

 data.⎕EX'wx'       ⍝ Delete outdated stuff

 :If 0=data.⎕NC'minimumAplVersion'
 :OrIf 0=≢data.minimumAplVersion
     data.minimumAplVersion←'18.0'
 :EndIf

 :If 0<data.⎕NC'info_url'
     data.project_url←data.info_url
     data.⎕EX'info_url'
 :EndIf

 tags←{','@(⍸';'=⍵)⊢⍵}data.tags
 tags←','(≠⊆⊢)tags
 tags~←'apl' 'dyalog' 'dyalog-apl' 'dyalogapl'   ⍝ Tatin is a Dyalog APL package manager...

 :If 0∊data.⎕NC↑'os_lin' 'os_mac' 'os_win'
     :If 0=data.⎕NC'os_win'
         data.os_win←(⊂'windows')∊tags
     :EndIf
     :If 0=data.⎕NC'os_win'
         data.os_mac←∨/'mac-os' 'mac' 'macos'∊tags
     :EndIf
     :If 0=data.⎕NC'os_win'
         data.os_lin←(⊂'linux')∊tags
     :EndIf
 :EndIf

 tags~←'windows' 'mac-os' 'macos' 'mac' 'linux'
 tags~←'apltree'groupName          ⍝ apltree does not offer much, and the group name should never be a tag
 data.tags←{0=≢⍵:'' ⋄ ⊃{⍺,',',⍵}/⍵}tags
⍝Done