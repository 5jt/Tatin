 {r}←OnHouseKeeping msg
⍝ Used for two purposes:
⍝ * Check ∆SHUTDOWN; if this is 1 sgut the server down
⍝ * Check whether the WS has been updated since it was loaded. If so reload.
 r←0
 :If ∆SHUTDOWN
     {}⎕EX'∆SHUTDOWN'
     :If ##.APLTreeUtils.IsDevelopment
         {}⎕NQ'#'QuitDQ
     :Else
         {}#.Plodder.Shutdown G.INI                                 ⍝ Will ⎕OFF in runtime
     :EndIf
 :ElseIf 0<⎕NC'∆STARTED'
 :AndIf 'CLEAR WS'≢⎕WSID
     :If ∆STARTED<1 ⎕DT⊃('type' 3)F.Dir'Server.dws'
         :If ⊃G.INI.Get'Config:ReloadWS'
             ⎕LOAD ⎕WSID
         :EndIf
     :EndIf
 :EndIf
 :If G.INI.Changed                                                  ⍝ In case the INI file got changed...
     ##.Plodder.G(##.Plodder.ProcessIniFile)←G.INI.IniFilename      ⍝ ... we initialize from scratch
 :EndIf
⍝Done
