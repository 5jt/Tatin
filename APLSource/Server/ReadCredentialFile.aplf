 r←ReadCredentialFile filename;buff
⍝ Read the credential file (either Credentials.txt or Credentials.csv), and remove any comments
⍝ (= lines that start with a semicolon) as well as empty lines.
⍝ Returns a matrix with 4 columns:
⍝ [1] Group name
⍝ [2] API key
⍝ [3] SALT (empty in case of Credentials.txt)
 r←0 4⍴⊂''
 :If F.IsFile filename
 :AndIf 0<≢buff←⊃F.NGET filename 1
 :AndIf 0<≢buff←(';'≠⊃¨buff)/buff
 :AndIf 0<≢buff←(0<≢¨buff)/buff
     buff←{','@(⍸'='=⍵)⊣⍵}¨buff  ⍝ Exchange the deprecated "=" against ","
     r←3↑⍤1⊢(↑','A.Split¨buff),⊂''
 :EndIf
