 response←DeletePackages request;b;deletePolicy;msg;msgs;noOfFailures;noOfOkay;okay;packageID;packageIDs;rc;regPath;statusCodes;⎕TRAP;packageID_
⍝ Delete one or more packages.\\
⍝ Whether deleting a package is an option is determined by the INI setting [CONFIG]DeletePackages
⍝ which is returned by `GetDeletePolicy` as a text vector.
 deletePolicy←GetDeletePolicy
 packageIDs←{','∊⍵:','(≠⊆⊢)⍵ ⋄ ⊆⍵}request.RequestTarget
 :If 'None'≡deletePolicy
     response←request Respond 405
     response.Content←'This server does not allow deleting packages'
 :ElseIf ~∧/b←Reg.IsValidPackageID_Complete¨packageIDs
     response←request Respond 400
     response.Content←'Invalid package ID(s): ',⊃{⍺,', ',⍵}/(~b)/packageIDs
 :Else
     statusCodes←msgs←''
     regPath←Reg.AddSlash G.RegistryPath
     :For packageID :In packageIDs
         :If 0=≢packageID_←regPath CorrectPkgName packageID
             statusCodes,←404
             msgs,←⊂'Package not found: ',packageID
         :Else
             :If 'JustBetas'≢deletePolicy
             :OrIf Reg.IsBeta packageID_
                 :If 0=≢msg←Reg.DeletePackage regPath,packageID_
                     statusCodes,←200
                     msgs,←⊂''
                 :Else
                     statusCodes,←400
                     msgs,←⊂msg
                 :EndIf
             :Else
                 statusCodes,←405
                 msgs,←⊂'Not a beta version but the server policies allows deletion of betas only'
             :EndIf
         :EndIf
     :EndFor
     okay←200=statusCodes
     :If ∨/okay
         (rc msg)←Emails.SendMail_DeletedPkg G.INI(okay/packageIDs)
         :If rc≠0
             P.AppLog⊆'*** Error'
             P.AppLog⊆'Sending email to gatekeeper with notification of a deleted package failed: ',(⍕∊rc)
         :EndIf
     :EndIf
     :If ∧/okay
         response←request Respond 200
         response.Content←'Package',((1<≢msgs)/'s'),' deleted'
         ##.Registry.Index.Compile G.RegistryPath
     :Else
         response←request Respond⌈/statusCodes
         noOfOkay←statusCodes+.=200
         noOfFailures←statusCodes+.≠200
         msg←¯2↓⊃,/{(1⊃⍵),': ',(⍕2⊃⍵),' (',(3⊃⍵),'); '}¨↓⍉↑(~okay)∘/¨packageIDs statusCodes msgs
         response.Content←(⍕noOfOkay),' package',((1≠noOfOkay)/'s'),' successesfully deleted. Failure',((1≠noOfFailures)/'s'),': ',msg
     :EndIf
 :EndIf
⍝Done
