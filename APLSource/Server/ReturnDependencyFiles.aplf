 response←request ReturnDependencyFiles packageIDs;json;data;html;qdmx;packageID;filename;b
⍝ Returns config files or ⍬ for every packageID
 :Trap Reg.ErrorNo
     :If IsJsonRequest request.Headers
         :If 0∊b←F.IsDir G.RegistryPath∘,¨⊆packageIDs
             response←R.NewResponse request
             response←request Respond 404 0
             response.Content←'Not found: ',⊃{⍺,',',⍵}/(~b)/⊆packageIDs
         :Else
             json←ReturnDependencyFiles_ packageIDs
             response←R.NewResponse request
             response.Content←json
         :EndIf
     :Else
         packageID←packageIDs               ⍝ For HTML it's a single one for sure
         filename←G.RegistryPath,packageID,'/',Reg.DependenciesFilename
         :If F.IsFile filename
             data←⊃F.NGET filename 1
             html←PrepareHtmlPageForDependencies data
             response←request ReturnHTML html
         :Else
             response←request Respond 404 1
         :EndIf
     :EndIf
 :Else
     qdmx←⎕DMX
     response←request Respond 400 0
     response.Content←qdmx.EM
 :EndTrap
⍝Done
