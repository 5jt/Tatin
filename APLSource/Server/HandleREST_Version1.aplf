 response←request HandleREST_Version1 path_;packageID;json;path;data;html;regFolder;specialCommands
⍝ Handles all commands defined in version 1 of the REST interface
 path_↓⍨←-'/'=¯1↑path_
 :If 1='/'+.=path_
     path←path_
     packageID←''
 :Else
     (path packageID)←Reg.SeparateUriAndPackageID path_
     path↓⍨←-'/'=¯1↑path
 :EndIf
 path←⎕C path
 specialCommands←'get-server-path' 'recompile-index' 'stop' 'shutdown' 'crash' 'list-commands'
 :If (⊂path)∊(⊂'v1/'),¨specialCommands
     response←request ProcessTestCommands(⎕C path)specialCommands
 :Else
     :Select path
     :Case GetVersion_REST'packages'
         response←request GetPackageList packageID
     :Case GetVersion_REST'packages/major_versions'
         regFolder←'expand'F.NormalizePath G.RegistryPath
         response←request GetMajorVersionList packageID regFolder
     :Case GetVersion_REST'packages/versions'
         response←request GetVersionList packageID
     :Case GetVersion_REST'packages/details'
         response←request GetDetails packageID
     :Case GetVersion_REST'packages/dependencies'
         response←request GetDependencies packageID
     :Case GetVersion_REST'packages/best_version'
         response←request GetBestVersionNumber packageID
     :Case GetVersion_REST'groups'
         regFolder←'expand'F.NormalizePath G.RegistryPath
         response←GetGroupList regFolder
     :Case GetVersion_REST'tags'
         response←GetTagList request
     :Case GetVersion_REST'documentation'
         response←CreateDocumentationIndex request
     :Else
         ⍝ Something new?
         response←request Respond 400 1 ⍝ Not a valid REST version 1 request
     :EndSelect
 :EndIf
⍝Done
