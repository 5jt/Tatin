 response←request GetPackageList packageID;data;html;noOfVersions;IDs;parms;data2;list;description;uniqueIDs
⍝ Respond to a request for some or all packages, HTML or JSON
 :If IsJsonRequest request.Headers
     parms←⎕NS''
     :If (⊂'group')∊request.QueryParameters[;1]
         parms.group←{2⊃⍵[⍵[;1]⍳⊂'group';]}request.QueryParameters
     :EndIf
     :If (⊂'tags')∊request.QueryParameters[;1]
         parms.tags←{2⊃⍵[⍵[;1]⍳⊂'tags';]}request.QueryParameters
     :EndIf
     data←Reg.ListPackages(G.RegistryPath,'/',packageID)parms
     response←request ReturnJSON Reg.JSON data
 :Else
     ⍝ Because it's a request from a Browser for the time
     ⍝ being we know that *all* packages are requested.
     data←0 Reg.ListPackages G.RegistryPath(⎕NS'')
     data2←∪Reg.RemoveMinorAndPatch¨data
     :If 2='-'+.=⊃data2
         noOfVersions←{≢⍵}⌸data2
     :Else
         noOfVersions←{≢⍵}⌸2⊃¨'-'A.Split¨data2
     :EndIf
     uniqueIDs←data[(¯1+1↓∪data2⍳data2),≢data2]
     description←↑FetchDescriptionAndPackageProjectUrl¨uniqueIDs
     data←(∪data2),description,⍕¨noOfVersions
     html←uniqueIDs PrepareHtmlPageForPackageList data
     response←request ReturnHTML html
 :EndIf
