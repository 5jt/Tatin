 response←request GetPackageList packageID;data;html;noOfVersions;IDs
⍝ Respond to a request for some or all packages, HTML or JSON
 :If IsJsonRequest request.Headers
     data←Reg.ListPackages G.RegistryPath,'/',packageID
     response←request ReturnJSON Reg.JSON data
 :Else
     ⍝ Because it's a request from a Browser for the time
     ⍝ being we know that *all* packages are requested.
     data←0 Reg.ListPackages G.RegistryPath
     noOfVersions←{≢⍵}⌸2⊃¨'-'A.Split¨data
     IDs←Reg.LatestVersions data
     :If 0<≢data←⍪Reg.RemoveVersionNumber¨IDs
         data,←↑FetchDescriptionAndPackageProjectUrl¨IDs
         data,←noOfVersions
     :EndIf
     IDs←1⊃¨'-'Reg.SplitAtLast¨IDs
     html←IDs PrepareHtmlPageForPackageList data
     response←request ReturnHTML html
 :EndIf
