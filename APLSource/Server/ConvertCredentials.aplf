credentials←credentials ConvertCredentials newCredentials;i;group;apiKey;salt;hash
⍝ Converts API-keys in `newCredentials` into hashes and adds a Salt along the way.
⍝ Eventually merges `credentials` and `newCredentials`.
 :If 0<≢newCredentials
     newCredentials,←⊂''
     :For i :In ⍳≢newCredentials
         (group apiKey)←2↑' '~⍨¨newCredentials[i;]
         :If 0=≢apiKey
             newCredentials[i;]←group'' ''
         :Else
             salt←HashPasswords.CreateSalt 10
             hash←HashPasswords.Hash salt,apiKey
             newCredentials[i;]←group hash salt
         :EndIf
     :EndFor
     :If 0=≢credentials
         credentials←newCredentials
     :Else
         credentials←(~credentials[;1]∊newCredentials[;1])⌿credentials
         credentials⍪←newCredentials
     :EndIf
 :EndIf
