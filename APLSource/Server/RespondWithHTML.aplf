 response←request RespondWithHTML y;html;msg;statusCode;caption
⍝ Compiles an HTML response with "caption" becoming an <h2> tag and
⍝ the (optional) "msg" one to many <p> tags.
 (statusCode caption msg)←3↑(⊆y),'' '' ''
 :If 0=≢caption
     caption←GetReasonPhraseFor statusCode
 :EndIf
 response←request Respond statusCode
 html←HtmlHeader''
 html,←⊂'<h2>',caption,' (',(⍕statusCode),')</h2>'
 html,←AddSpecialMessage ⍬
 :If 0<≢msg
     html,←{'<p>',⍵,'</p>'}¨⊆msg
 :EndIf
 html,←'</body>' '</html>'
 response.Content←MassageHTML html
⍝Done
