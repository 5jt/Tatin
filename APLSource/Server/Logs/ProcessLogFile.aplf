 {r}←ProcessLogFile data;date;packageIDs;months;months2;date2;Reg
⍝ Takes the data saved in the log file and reduces them to the essentials, aggrgate just by day
 r←0 4⍴⊂''
 Reg←##.##.Registry
 :If 0<≢data←('Conga:'{⍺∘≢¨(≢⍺)↑¨data}data)⌿data
 :AndIf 0<≢data←(∨/¨'] "GET /'∘⍷¨data)/data
 :AndIf 0<≢data←(' - - '∘{(≢⍺)+1⍳⍨⍺⍷⍵}¨data)↓¨data
 :AndIf 0<≢data←(~∨/¨'HTTP/1.1" 404'∘⍷¨data)/data
 :AndIf 0<≢date←(-≢' 0000')↓¨({¯1+⍵⍳']'}1⊃data)↑¨data
     packageIDs←('"get '{(≢⍺)+1⍳⍨⍺⍷⎕C ⍵}1⊃data)↓¨data
     packageIDs←(¯1+packageIDs⍳¨' ')↑¨packageIDs
     packageIDs←↑{(⍵↑⍨¯1+⍵⍳'-')({⍵↑⍨¯1+⍵⍳'-'}⍵↓⍨⍵⍳'-')(⍵↓⍨1++/2>+\'-'=⍵)}¨packageIDs
     months←↓12 3⍴'JanFebMarAprMayJunJulAugSepOctNovDec'
     months2←↓12 2⍴'010203040506070809101112'
     date2←(4↑¨7↓¨date),[1.5](months2[months⍳{3↑⍵↓⍨⍵⍳'/'}¨date])
     r←packageIDs,date2
     r←r[⍒r[;4];]
     r←r[⍋r[;3];]
     r←r[⍋r[;2];]
     r←r[⍋r[;1];]
     r←{⍺,≢⍵}⌸r
 :EndIf
 ⍝Done
