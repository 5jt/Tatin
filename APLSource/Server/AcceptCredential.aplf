 okayFlag←credentials AcceptCredential(credential groupName);groupNames;credentials;ind
⍝ Takes an API key and a group name and checks whether that is found in the array `credentials`.
⍝ Note that `⍺` is usually provided as `G.Credentials` (except test cases etc.).\\
⍝ This function is called when the user attempts to perform a PUT (read: "Publish") or
⍝ a DELETE (if ever) operation.
 okayFlag←0
 :If 0<≢credentials                                     ⍝ Do we have any credentials saved at all?
     :If 0=≢credential                                  ⍝ Did the user provide credentials
         okayFlag←(,'*')≡(∊credentials)~⎕TS             ⍝ Everybody anything?
     :Else
         (groupNames credentials)←credentials
         ind←credentials⍳⊂credential
         :If ind≤≢credentials                           ⍝ Must be known, otherwise weare done
             :If (⊂groupName)∊groupNames                ⍝ Was that group assigned a specific API kay?
                 okayFlag←groupName≡ind⊃groupNames      ⍝ Then it must be that specific group
             :Else
                 okayFlag←(,'*')≡,ind⊃groupNames        ⍝ Is it a "master kay" (all groups)?
             :EndIf
         :EndIf
     :EndIf
 :EndIf
⍝Done
