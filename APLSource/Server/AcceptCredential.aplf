 okayFlag←credentials AcceptCredential(apiKey groupName);credentials;ind;salt;hash;apiKeys;groupNames
⍝ Takes an API key and a group name and checks whether that is found in the array `credentials`.
⍝ Note that `G.Credentials` is usually provided as `⍺` except test cases etc.\\
⍝ This function is called when the user attempts to perform a PUT (read: "Publish") or
⍝ a DELETE (if ever) or any other operation that requires authentication.\\
⍝ apiKey must be clear text.
⍝ Note that with version this function also checks whether for a group name that is not * and
⍝ requires an API key there is an email address defined.
 okayFlag←1
 :If 0<≢credentials                                         ⍝ Do we have any credentials saved at all?
     :If 2=⍴⍴credentials
         (groupNames apiKeys)←↓⍉credentials[;1 2]
         :If 0=≢apiKeys~' '
             okayFlag←1
         :Else
             apiKey←A.DTB A.DLB apiKey
             :If 0=≢apiKey                                      ⍝ Did the user provide credentials
                 :If (⊂groupName)∊groupNames                    ⍝ Does this group NOT require an API key?
                     okayFlag←1
                 :Else
                     okayFlag←(,'*')≡(∊credentials)~⎕TS,' '     ⍝ Everybody anything?
                 :EndIf
             :Else
                 ind←groupNames⍳⊂,groupName
                 :If ind≤≢groupNames                            ⍝ Is it known?!
                     :If 0<≢ind⊃apiKeys                         ⍝ Was that group assigned a specific API kay?
                         salt←⊃credentials[ind;3]
                         hash←HashPasswords.Hash salt,apiKey
                         okayFlag←hash≡2⊃credentials[ind;]      ⍝ Check the api key hash
                     :Else
                         okayFlag←(,'*')≡,ind⊃groupNames        ⍝ Is it a "master key" (all groups)?
                     :EndIf
                 :Else
                     ind←groupNames⍳⊂,'*'                       ⍝ Is there...
                     :If ind≤≢groupNames                        ⍝ ... a wildcard?
                         salt←⊃credentials[ind;3]
                         hash←HashPasswords.Hash salt,apiKey
                         okayFlag←hash≡2⊃credentials[ind;]      ⍝ Check the api key hash
                     :EndIf
                 :EndIf
             :EndIf
         :EndIf
     :EndIf
 :EndIf
⍝Done
