 G←ProcessCredentials G;filename;credentials;body;newCredentialsFilename;newCredentials
⍝ Returns a matrix with {group} - Salt - API-key-hash combinations used for authentication.
 newCredentialsFilename←(F.AddTrailingSep G.RegistryPath),'Credentials.txt'
 filename←(F.AddTrailingSep G.RegistryPath),'Credentials.csv'
 :If 0=F.Exists filename
     ''F.NPUT filename
 :EndIf
 :If 0=≢credentials←ReadCredentialFile filename
     G.Credentials←0 3⍴''
 :ElseIf (,'*')≡(∊credentials)~⎕TC,','
     G.Credentials←1 3⍴'*' '' ''
 :Else
     G.Credentials←credentials
 :EndIf
 :If 0<≢newCredentials←ReadCredentialFile newCredentialsFilename
     G.Credentials←G.Credentials ConvertCredentials newCredentials
     (⊂⊃¨{⍺,',',⍵}/¨↓G.Credentials)F.NPUT filename 1
     F.DeleteFile newCredentialsFilename
 :EndIf
 G.CredentialsTimestamp←Reg.Timestamp2Float⊃⊃('type' 3)F.Dir filename
⍝Done
