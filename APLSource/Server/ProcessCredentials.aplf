 G←ProcessCredentials G;filename;credentials;body;oldFilename;newCredentials
⍝ Returns a matrix with {group} - Salt - API-key-hash combinations used for authentication.
⍝ For the time being, if the file is still lacking an email address, no conversion is done.
 oldFilename←G.RegistryPath,'/Credentials.txt'
 filename←G.RegistryPath,'/Credentials.csv'
 :If 0=F.Exists filename
     ''F.NPUT filename
 :EndIf
 :If 0=≢credentials←ReadCredentialFile filename
     G.Credentials←0 3⍴''
 :ElseIf (,'*')≡(∊credentials)~⎕TC,','
     G.Credentials←1 3⍴'*' '' ''
 :Else
     G.Credentials←credentials
 :EndIf
 :If 0<≢newCredentials←ReadCredentialFile oldFilename
     G.Credentials←G.Credentials ConvertCredentials newCredentials
     (⊂⊃¨{⍺,',',⍵}/¨↓G.Credentials)F.NPUT filename 1
     F.DeleteFile oldFilename
 :EndIf
 G.CredentialsTimestamp←Reg.Timestamp2Float⊃⊃('type' 3)F.Dir filename
⍝Done
