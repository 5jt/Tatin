 response←Handle_GET request;html;packageID;fullPath;path;versionNo
 :If (,'/')≡request.Path
 :OrIf '/index.html'≡request.Path
     response←CreateHomePage request
 :Else
     fullPath←{⍵↓⍨'/'=⊃⍵}request.Path
     :If 'Assets/'≡{⍵↑⍨⌊/⍵⍳'/\'}(≢Reg.VersionOf_REST)↓fullPath  ⍝ Static stuff with REST version
         response←request ReturnFiles G.HTDOCS,(≢Reg.VersionOf_REST)↓fullPath
     :ElseIf 'Assets/'≡{⍵↑⍨⌊/⍵⍳'/\'}fullPath                    ⍝ Static stuff without REST version
         response←request ReturnFiles G.HTDOCS,fullPath
     :ElseIf 'usage-data/'≡{⍵↑⍨⌊/⍵⍳'/\'}fullPath
         response←request ReturnUsageDataFiles fullPath
     :Else
         path←Reg.RemovePackageID fullPath
         packageID←(≢path)↓fullPath
         :If 0=≢path
             :If Reg.IsValidPackageID_Complete packageID
                 response←request FetchPackage packageID        ⍝ It is a fully qualified packageID
             :Else
                 ⍝ Neither a valid package ID nor a proper REST request
                 :If (⊂'text/html')∊request.Accept.MediaRange   ⍝ Is it a browser?
                     response←request RespondWithHTML 404 'Invalid package ID'
                 :Else
                     response←request Respond 404               ⍝ Nope, so we send back JSON5
                 :EndIf
             :EndIf
         :ElseIf request IsREST_v1 fullPath
             response←request Handle_GET_REST_Version1 fullPath
         :Else
             response←request Respond 404                       ⍝ Neither a valid package ID nor a proper REST request
         :EndIf
     :EndIf
 :EndIf
⍝Done
