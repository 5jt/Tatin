 r←Test_UC_242(stopFlag batchFlag);⎕TRAP;list1;list2;msg;rc;stop;tempDir
⍝ Copy some specific packages, first without, then with dependencies
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 tempDir←∆GetTempDir⊃⎕SI
 stop←0
 (rc msg list1)←stop ∆UCMD'CopyRegistry [localhost] "',tempDir,'" -verbose=1 -list=aplteam-OS,aplteam-rideclient -dependencies=0 -latest'
 msg Assert 0=rc
 →T.GoToTidyUp 2≠≢list1

 (rc msg list2)←stop ∆UCMD'CopyRegistry [localhost] "',tempDir,'" -verbose=1 -list=aplteam-OS,aplteam-rideclient -latest'
 msg Assert 0=rc

 →T.GoToTidyUp~</≢¨list1 list2
 →T.GoToTidyUp~∧/⎕NEXISTS¨(⊂tempDir,'/'),¨list1[;1]

 r←T._OK

∆TidyUp:
 (rc msg)←F.RmDirByForce tempDir
 msg Assert 0=rc
⍝Done
