 r←Test_Load_061(stopFlag batchFlag);⎕TRAP;refs;bestVersion;versions
⍝ Try to load a particular package without specifying a registry and without a patch number
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS'' ⍝ Used to load stuff into

 refs←TC.LoadPackage'aplteam-APLProcess-0.2'#.TEMP
 →T.GoToTidyUp 0=≢refs
 versions←{⊃,/1↓⎕NPARTS ⍵}¨⊃F.Dir ∆GetServerRegistryFolder,'aplteam-APLProcess-0.2*'
 bestVersion←{⍵↑⍨{1+-⍵⍳'-'}⌽⍵}⊃¯1↑{⍵[Reg.SortIndexForPackageIDs ⍵]}versions
 →T.GoToTidyUp bestVersion{⍺≢(≢⍺)↑⍵}2⊃#.TEMP.APLProcess.Version

 r←T._OK

∆TidyUp:
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
