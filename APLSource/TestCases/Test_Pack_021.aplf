 r←Test_Pack_021(stopFlag batchFlag);⎕TRAP
 ;dir;src;dist;cfg;zipfile;ns;qdmx
⍝ Pack a code folder together with a text file and a sub-directory
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX P.GetTatinRootSpace'#'
 dir←GetTempDir ⍬
 (src dist)←(⊂dir,'/'),¨'src' 'dist'
 src,←'/v0.1.0'
 F.MkDir(src,'/source')dist(src,'/docs')

 ('foo1←{1}')F.NPUT src,'/source/foo1.apln'
 cfg←P.InitPackageConfig ⍬
 cfg.group←'Example'
 cfg.name←'Package_B'
 cfg.version←'0.1.0'
 (F.ExpandPath src,'/README.md')⎕NCOPY F.ExpandPath'README.md'
 (F.ExpandPath src,'/docs/Managing_packages_with_Tatin.md')⎕NCOPY F.ExpandPath'docs/Managing_packages_with_Tatin.md'
 cfg.files←'README.md' '/docs/Managing_packages_with_Tatin.md'
 cfg P.WritePackageConfigFile src

 :Trap 98
     {}P.Pack src dist
     →T.GoToTidyUp
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp~'"cfg.source" is undefined and cannot be established'∆LazyMatch qdmx.EM
 :EndTrap

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 #.⎕EX P.GetTatinRootSpace'#'
⍝Done
