 r←Test_API_050(stopFlag batchFlag);⎕TRAP;dir;US;myReg1;myReg2
⍝ Exercise ⎕SE.Tatin.UserSettings and ⎕SE.Tatin.DefineRegistry: create a temporay user config file
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 dir←GetTempDir''

 US←⎕NEW ⎕SE.Tatin.UserSettings(,⊂dir)

 myReg1←⎕NEW ⎕SE.Tatin.DefineRegistry(,⊂'[my]https://MyCompany.com/')
 →T.GoToTidyUp'my'≢myReg1.alias
 →T.GoToTidyUp'https://MyCompany.com/'≢myReg1.uri
 US.AddRegistry myReg1

 myReg2←⎕NEW ⎕SE.Tatin.DefineRegistry(,⊂'[tatin]https://tatin.dev/')
 →T.GoToTidyUp'tatin'≢myReg2.alias
 →T.GoToTidyUp'https://tatin.dev/'≢myReg2.uri
 US.AddRegistry myReg2

 →T.GoToTidyUp'my' 'tatin'≢US.registries.alias
 →T.GoToTidyUp'https://MyCompany.com/' 'https://tatin.dev/'≢US.registries.uri

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
⍝Done
