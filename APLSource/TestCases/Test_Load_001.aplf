 r←Test_Load_001(stopFlag batchFlag);⎕TRAP
 ;dir;src;zip;body;cfg;zipfile;refs
⍝ Pack a package into a ZIP file and then load it. Must not leave anything behind in the file system.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX P.GetTatinRootSpace'#'
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS'' ⍝ Used to load stuff into
 dir←GetTempDir ⍬
 (src zip)←(⊂dir,'/'),¨'src' 'reg'
 F.MkDir src zip

 body←':Namespace Constants' '∇r←PI' 'r←3.14' '∇' ':EndNamespace'
 (⊂body)F.NPUT src,'/Constants.apln'
 cfg←P.InitPackageConfig ⍬
 cfg.(group name version)←'Example' 'Constants' '0.1.0'
 cfg.source←'Constants.apln'
 cfg P.WritePackageConfigFile src
 zipfile←P.Pack src zip

 refs←P.LoadPackage('file://',zipfile)#.TEMP
 Assert 3=#.TEMP.Constants.⎕NC'PI'
 →T.PassesIf 3.14=#.TEMP.Constants.PI
 →T.PassesIf 0<≢#.TEMP.Constants.##.∆URI
 →T.PassesIf 0=≢#.TEMP.Constants.##.∆HOME
 →T.PassesIf 1=≢refs

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 #.⎕EX P.GetTatinRootSpace'#'
⍝Done
