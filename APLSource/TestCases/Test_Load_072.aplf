 r←Test_Load_072(stopFlag batchFlag);⎕TRAP;qdmx
 ;dir;src;zip;body;cfg;zipfile
⍝ Pack a package with a niladic function as API and try to load it (niladic functions are not permitted as API)
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS'' ⍝ Used to load stuff into
 dir←GetTempDir ⍬
 (src zip)←(⊂dir,'/'),¨'src' 'reg'
 F.MkDir src zip

 body←'∇Hello' '⎕←''Hello world''' '∇'
 (⊂body)F.NPUT src,'/hello.aplf'
 cfg←TC.InitPackageConfig ⍬
 cfg.(group name version)←'Example' 'Hello' '0.1.0'
 cfg.source←'Hello.aplf'
 cfg.api←'Hello'
 cfg TC.WritePackageConfigFile src
 zipfile←TC.Pack src zip

 :Trap 98
     {}TC.LoadPackage('file://',zipfile)#.TEMP
     →T.FailsIf 1
 :Else
     qdmx←⎕DMX
     →T.PassesIf∨/'entry point is niladic function'⍷qdmx.EM
 :EndTrap

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
⍝Done
