 r←Test_CheckVersions_060(stopFlag batchFlag);⎕TRAP;dir;versions;result;oldPriorities;json;RmDir;qdmx
⍝ Run CheckForLaterVersion on package from Registry not on Tatin Registry search path (priority←0)
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 dir←∆GetTempDir''
 dir ⎕NCOPY⍠('Wildcard' 1)⊣'./TestData/myproj/*'

 json←##.Client.MyUserSettings.GetRegistry'https://test.tatin.dev/'
 oldPriorities←json.priority
 json.priority←0
 ##.Client.MyUserSettings.ReplaceRegistry json

 :Trap 998
     {}1 TC.CheckForLaterVersion dir,'/packages'
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp'The build list carries entries that are not on the Tatin Registry search path'≢qdmx.EM
 :EndTrap

 r←T._OK

∆TidyUp:
 F.RmDir dir
 json.priority←oldPriorities
 ##.Client.MyUserSettings.ReplaceRegistry json
⍝Done
