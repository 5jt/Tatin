 r←Test_UC_162(stopFlag batchFlag);⎕TRAP;rc;msg;tempDir;packageFolder;zipfile;cfg
⍝ Exercise ]Tatin.BuildPackage with an invalid setting of `api`
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 tempDir←∆GetTempDir''
 tempDir ⎕NCOPY'TestData/Build/FilesAndDirs'
 packageFolder←tempDir,'/FilesAndDirs'

 cfg←⎕JSON⍠('Dialect' 'JSON5')⊣⊃⎕NGET packageFolder,'/apl-package.json'
 cfg.api←'Does_Not_Exist' ⍝ Invalid: must be a namespace or a script
 (⊂⎕JSON⍠('Dialect' 'JSON5')('Compact' 0)⊣cfg)⎕NPUT(packageFolder,'/apl-package.json')1

 (rc msg zipfile)←∆UCMD'BuildPackage "',packageFolder,'" "',tempDir,'"'
 :If rc=0
     {}F.DeleteFile zipfile
     →T.GoToTidyUp 1
 :Else
     →T.GoToTidyUp rc≠911
     →T.GoToTidyUp~∨/'API does not exist'⍷msg
 :EndIf

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDirByForce tempDir
⍝Done
