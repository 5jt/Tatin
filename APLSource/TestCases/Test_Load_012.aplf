 r←Test_Load_012(stopFlag batchFlag);⎕TRAP
 ;dir;src;reg1;reg2;body;cfg;zipfile;refs;path
⍝ Publish simple package to the second registry and then load it from there using the folder name.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX P.GetTatinRootSpace'#'
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS'' ⍝ Used to load stuff into
 dir←GetTempDir ⍬
 (src reg1 reg2)←(⊂dir,'/'),¨'src' 'reg1' 'reg2'
 F.MkDir src reg1 reg2

 P.Reg.CreateRegistry reg1
 P._UserSettings.AddRegistry'[Private]',reg1
 P.Reg.CreateRegistry reg2
 P._UserSettings.AddRegistry'[local2]',reg2

 body←':Namespace Constants' '∇r←PI' 'r←3.14' '∇' ':EndNamespace'
 (⊂body)F.NPUT src,'/Constants.apln'
 cfg←P.InitPackageConfig ⍬
 cfg.(group name version)←'Example' 'Constants' '1.0.0'
 cfg.source←'Constants.apln'
 cfg P.WritePackageConfigFile src
 zipfile←P.Pack src src
 P.PublishPackage zipfile'[local2]'

 path←dir,'/reg2/',⊃{⍺,'-',⍵}/cfg.(group name version)
 refs←P.LoadPackage('file://',path)#.TEMP

 →T.GoToTidyUp'Example_Constants_1_0_0'≢⊃' '~⍨¨↓#._tatin.⎕NL 9
 →T.GoToTidyUp 1≠≢F.ListDirs reg2
 →T.GoToTidyUp 1≠≢refs

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 Assert P._UserSettings.RemoveRegistry'[Private]'
 Assert P._UserSettings.RemoveRegistry'[local2]'
 #.⎕EX P.GetTatinRootSpace'#'
⍝Done
