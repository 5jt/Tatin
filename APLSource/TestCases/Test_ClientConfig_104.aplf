 r←Test_ClientConfig_104(stopFlag batchFlag);⎕TRAP;tempDir;settings;reg1;reg2;qdmx
⍝ Exercise ReplaceRegistry on a registry that does not exist
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 tempDir←GetTempDir''
 F.DeleteFile tempDir,'/',P.UserSettings.cfg_name                           ⍝ Precaution

 settings←⎕NEW P.UserSettings(,⊂tempDir)
 →T.GoToTidyUp 0≠≢settings.registries

 reg1←⎕NEW P.DefineRegistry(,⊂'https://localhost')
 reg1.alias←'foo'
 reg1.api_key←'Firlefanz'

 :Trap 98
     settings.ReplaceRegistry reg1
     →T.FailsIf 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp'There is no such registry yet'≢qdmx.EM
 :EndTrap

 reg2←⎕NEW P.DefineRegistry(,⊂'https://tatin.dev')
 settings.AddRegistry reg2
 →T.GoToTidyUp 1≠≢settings.registries

 :Trap 98
     settings.ReplaceRegistry reg1
     →T.FailsIf 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp'There is no such registry yet'≢qdmx.EM
 :EndTrap

 r←T._OK

∆TidyUp:
 F.RmDir tempDir
