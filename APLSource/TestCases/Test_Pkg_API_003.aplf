 r←Test_Pkg_API_003(stopFlag batchFlag);⎕TRAP;installFolder;noOf;rc;msg;zipFilename;src_;src_1;src_2;body
 ;dir;src;cfg;reg;list;index;expected;zipped;refs
⍝ Publish and load a package with two fns in an ordinary namespace and one fn in a class
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS''    ⍝ Used to create stuff
 #.⎕SHADOW'TEMP2' ⋄ 'TEMP2'#.⎕NS''  ⍝ Used to load stuff into
 dir←GetTempDir ⍬
 (src reg)←(⊂dir,'/'),¨'src' 'reg'
 src_←src,'/APLSource'
 F.MkDir src_ reg
 F.MkDir src_1←src_,'/MyNs'

 TC.Reg.CreateRegistry reg
 TC.MyUserSettings.AddRegistry'[local2]',reg

 #.TEMP.⎕FX'r←fns1 arg' 'r←1+arg'
 #.TEMP.⎕FX'r←fns2 arg' 'r←2+arg'
 #.TEMP.⎕FX'r←fns3 arg' 'r←3+arg'
 #.TEMP.⎕FX'r←fns4 arg' 'r←4+arg'
 #.TEMP.⎕FX'r←(fns op1) arg' 'r←fns 10+arg'
 #.TEMP.⎕FX'r←(fns op2) arg' 'r←fns 20+arg'
 #.TEMP.⎕FX'r←Run dummy' 'r←(fns1 100)+(fns2 100)'
 #.TEMP.⎕FX'r←RunAlso dummy' 'r←(fns3 100)+(fns4 100)'
 ∆ExportNamespace #.TEMP src_1

 #.⎕EX'TEMP' ⋄ #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS''    ⍝ Used to create stuff
 body←''
 body,←'∇r←Fns1 dummy' ':Access Public Shared' 'r←3.14' '∇'
 body,←'∇r←Fns2 dummy' ':Access Public Shared' 'r←3.1415' '∇'
 body←(⊂':Class MyClass'),body,(⊂':EndClass')
 #.TEMP.⎕FIX body
 body←''
 body,←'∇r←Sum y' ':Access Public Shared' 'r←+/y' '∇'
 body,←'∇r←Product y' ':Access Public Shared' 'r←×/y' '∇'
 body←(⊂':Class Utils'),body,(⊂':EndClass')
 #.TEMP.⎕FIX body
 ∆ExportClasses #.TEMP src_

 cfg←TC.InitPackageConfig ⍬
 cfg.(group name version)←'Example' 'A' '1.0.0'
 cfg.api←'MyNs.Run,MyClass.Fns2'
 cfg TC.WritePackageConfigFile src
 (rc msg zipFilename)←TC.PublishPackage src reg
 Assert 200=rc

 installFolder←GetTempDir'/Tatin_AppPackages'
 refs←TC.InstallPackage('[local2]Example-A-1.0.0')installFolder
 noOf←≢TC.LoadDependencies installFolder #.TEMP2

 →T.GoToTidyUp 1≠noOf
 →T.GoToTidyUp 9≠#.TEMP2.⎕NC'A'
 →T.GoToTidyUp 3≠#.TEMP2.⎕NC'A.MyNs.Run'
 →T.GoToTidyUp 3≠#.TEMP2.⎕NC'A.MyClass.Fns2'
 →T.GoToTidyUp 203≠#.TEMP2.A.MyNs.Run 100
 →T.GoToTidyUp 3.1415≠#.TEMP2.A.MyClass.Fns2 ⍬

 r←T._OK

∆TidyUp:
 Assert 0=1⊃F.RmDir dir
 Assert TC.MyUserSettings.RemoveRegistry'[local2]'
 #.⎕EX TC.GetTatinRootSpace'#'
 F.DeleteFile zipFilename
⍝Done
