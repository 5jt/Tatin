 r←Test_InstallAndLoad_033(stopFlag batchFlag);⎕TRAP;src4;list1;list2
 ;dir;src;body;cfg;installFolder;src2;src3;reg
⍝ Two packages A<B and two packages C<B - make sure that B is installed and loaded only once
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS'' ⍝ Used to load stuff into
 dir←GetTempDir ⍬
 (src reg)←(⊂dir,'/'),¨'src' 'reg'
 src2←src,'/Example-Constants-0.1.0/'
 src3←src,'/Example-MyMath-0.2.0/'
 src4←src,'/Example-MyTests-0.3.0/'
 F.MkDir reg
 F.MkDir src2 src3 src4,¨⊂'/APLSource'

 TC.Reg.CreateRegistry reg
 TC.MyUserSettings.AddRegistry'[local2]',reg

 body←':Class Constants' '∇r←PI' ':Access Public Shared' 'r←3.14' '∇' ':EndClass'
 (⊂body)F.NPUT src2,'/APLSource/Constants.aplc'
 cfg←TC.InitPackageConfig ⍬
 cfg.(group name version)←'Example' 'Constants' '0.1.0'
 cfg.source←'APLSource'
 cfg TC.WritePackageConfigFile src2
 TC.PublishPackage src2 reg

 body←⊂':Namespace MyMath'
 body,←'∇r←x Times y' 'r←##.Constants.PI × x × y' '∇'
 body,←'∇r←Total x' 'r←+/x' '∇'
 body,←'∇r←x Power y' 'r←x*y' '∇'
 body,←⊂':EndNamespace'
 (⊂body)F.NPUT src3,'/APLSource/Power.apln'
 cfg←TC.InitPackageConfig ⍬
 cfg.(group name version)←'Example' 'MyMath' '0.2.0'
 cfg TC.WritePackageConfigFile src3
 src3 TC.Dependencies.Add'[local2]Example-Constants-0.1.0'
 TC.PublishPackage src3 reg

 body←⊂':Namespace MyTests'
 body,←'∇r←x Test1 y' 'r←##.Constants.PI × x × y' '∇'
 body,←'∇r←Test2 x' 'r←+/x' '∇'
 body,←'∇r←x Test3 y' 'r←x*y' '∇'
 body,←⊂':EndNamespace'
 (⊂body)F.NPUT src4,'/APLSource/Test3.apln'
 cfg←TC.InitPackageConfig ⍬
 cfg.(group name version)←'Example' 'MyTests' '0.3.0'
 cfg TC.WritePackageConfigFile src4
 src4 TC.Dependencies.Add'[local2]Example-Constants-0.1.0'
 TC.PublishPackage src4 reg

 installFolder←GetTempDir'/Tatin_AppPackages'
 list1←TC.InstallPackage('[local2]Example-MyMath-0.2.0')installFolder
 →T.GoToTidyUp 2≠≢list1
 list2←TC.InstallPackage('[local2]Example-MyTests-0.3.0')installFolder
 →T.GoToTidyUp 1≠≢list2

 →T.GoToTidyUp 3≠+/F.IsDir⊃F.Dir installFolder,'/*'
 →T.GoToTidyUp 2≠≢TC.Dependencies.Get installFolder

 {}TC.LoadDependencies installFolder #.TEMP
 →T.GoToTidyUp 2≠≢#.TEMP.⎕NL⍳16
 →T.GoToTidyUp(3.14×3×4)≠3 #.TEMP.MyMath.Times 4
 →T.GoToTidyUp 7≠#.TEMP.MyMath.Total 3 4
 →T.GoToTidyUp(2*3)≠2 #.TEMP.MyMath.Power 3
 →T.GoToTidyUp(3.14×3×4)≠3 #.TEMP.MyTests.Test1 4
 →T.GoToTidyUp 7≠#.TEMP.MyTests.Test2 3 4
 →T.GoToTidyUp(2*3)≠2 #.TEMP.MyTests.Test3 3
 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 Assert TC.MyUserSettings.RemoveRegistry'[local2]'
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
