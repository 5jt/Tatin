 r←Test_Server_105(stopFlag batchFlag);⎕TRAP;zipFilename
 ;packageID;serverPath;qdmx;zip;cfg;body;clientCfg;dir;src;res
⍝ Publish a package twice - the second time must result in an error
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX P.GetTatinRootSpace'#'
 dir←GetTempDir ⍬
 (src zip)←(⊂dir,'/'),¨'src' 'zip'
 F.MkDir src zip
 serverPath←∆GetServerPath

 body←':Namespace Test' 'foo←{' '⍵' '}' ':EndNamespace'
 (⊂body)F.NPUT src,'/Test.apln'
 cfg←P.InitPackageConfig ⍬
 cfg.group←'Example'
 cfg.name←'Package_A'
 cfg.version←'0.1.0'
 cfg.source←'Test.apln'
 cfg P.WritePackageConfigFile src

 packageID←'Example-Package_A-0.1.0'
 zipFilename←P.PublishPackage src('https://localhost/',packageID)

 :Trap 98
     P.PublishPackage src('https://localhost/',packageID)
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp'The package has already been published'≢qdmx.EM
 :EndTrap

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 Assert 0=⊃F.RmDir serverPath,'/',packageID
 F.DeleteFile zipFilename
 ∆RecompileIndexOnServer
⍝Done
