 {r}←UpdateTatin Arg;path;filename;folder;path2Config;version;q;folder2;GH;currentVersion;tag;msg
 r←''
 folder←1⊃⎕NPARTS GetSourceFile
 :If ~{{⍺≡(≢⍺)↑⍵}/'expand'∘TC.F.NormalizePath¨⍵}(1 TC.GetProgramFilesFolder'')folder
     ('Cannot deal with ',folder)Assert{{⍺≡(≢⍺)↑⍵}/'expand'∘TC.F.NormalizePath¨⍵}(0 TC.GetProgramFilesFolder'')folder
 :EndIf
 GH←⎕NEW TC.##.GitHubAPIv3(,⊂'aplteam')
 filename←folder,'/../../GitHub-personal-token.txt'
 :If TC.F.IsFile filename
     GH.personal_access_token←∊⊃TC.F.NGET filename 1
 :EndIf
 currentVersion←({⍵↑⍨¯1+⌊/⍵⍳'+-'}2⊃TC.Version)
 tag←GH.CheckForUpdate'Tatin'currentVersion
 :If 0=≢tag
     r←'Tatin is up-to-date: ',⊃{⍺,' from ',⍵}/1↓TC.Version
 :Else
     msg←'UpdateTatin@Your version of Tatin is ',{⍵↑⍨¯1+⍵⍳'+'}2⊃TC.Version
     msg,←(⎕UCS 13),'Latest published version is ',1↓tag
     msg,←(⎕UCS 13),'Would you like to download and install ',(1↓tag),' ?'
     :If 1 TC.YesOrNo msg
         version←TC.UpdateClient tag folder
         :If 0<≢version
             {}⎕SE.UCMD'Open ','"',folder,'/Assets/docs/ReleaseNotes.html"'
             r←'Tatin updated on disk to ',version
             r,←CR,'The current WS has NOT been updated, please restart a fresh session.'
         :EndIf
              ⍝ Finally we check whether there is a folder Tatin/ in MyUCMDs/
         folder2←TC.GetMyUCMDsFolder'Tatin'
         :If TC.F.IsDir folder2
             q←'RemoveTatinFromMyUCMDs@'
             q,←'There is a folder ',folder2,' on disk - do you want the folder removed?'
         :AndIf 1 TC.C.YesOrNo q
             {}TC.F.RmDirByForce folder2
         :EndIf
         r←'Tatin was successfully updated to version ',(1↓tag),' in',(⎕UCS 13),'  ',folder2,(⎕UCS 13),'Now restart APL'
     :EndIf
 :EndIf
