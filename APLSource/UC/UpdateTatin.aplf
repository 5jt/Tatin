 {r}←UpdateTatin Arg;path;filename;folder;path2Config;version;q;folder2;GH;currentVersion;tag;msg
 r←''
 folder←1⊃⎕NPARTS GetSourceFile
 GH←⎕NEW TC.##.GitHubAPIv3(,⊂'aplteam')
 filename←folder,'/../../GitHub-personal-token.txt'
 :If TC.F.IsFile filename
     GH.personal_access_token←∊⊃TC.F.NGET filename 1
 :EndIf
 currentVersion←({⍵↑⍨¯1+⍵⍳'+'}2⊃TC.Version)
 tag←GH.CheckForUpdate'Tatin'currentVersion
 :If 0=≢tag
     r←'There is no later version available than ',⊃{⍺,' from ',⍵}/1↓TC.Version
 :Else
     msg←'UpdateTatin@Your version of Tatin is ',{⍵↑⍨¯1+⍵⍳'+'}2⊃TC.Version
     msg,←CR,'Latest published version is ',1↓tag
     msg,←CR,'Would you like to download and install ',(1↓tag),' ?'
     :If 1 TC.YesOrNo msg
         version←TC.UpdateClient tag folder
         :If 0<≢version
             0 TC.A.GoToWebPage'file://',folder,'/Assets/docs/ReleaseNotes.html'
         :EndIf
         ⍝ Finally we check whether there is a folder Tatin/ in MyUCMDs/
         folder2←TC.GetMyUCMDsFolder'Tatin'
         :If TC.F.IsDir folder2
             q←'RemoveTatinFromMyUCMDs@'
             q,←'There is a folder ',folder2,' on disk.',CR
             q,←'That''s where Tatin was previously installed.',CR
             q,←'Do you want that folder to be removed?'
         :AndIf 1 TC.C.YesOrNo q
             {}TC.F.RmDirByForce folder2
         :EndIf
         r←'Tatin was successfully updated to version ',(1↓tag),' in',CR,'  ',folder,CR,'Now restart APL'
     :EndIf
 :EndIf
