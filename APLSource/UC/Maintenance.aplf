 r←Maintenance Arg;dry;path;home;⎕TRAP;show;filename;body;list;ind;info
 (dry show info)←Arg.(dry show info)
 path←Arg._1
 home←1⊃⎕NPARTS GetSourceFile
 :If info
     r←''
     'Could not find maintenece''s "README" file'Assert ⎕NEXISTS home,'Maintenance/README'
     {{}(#.⎕NS'').(⎕ED⍠('ReadOnly' 1)&{'ed'}ed←⍵)}⊃⎕NGET(home,'Maintenance/README')1
 :ElseIf show
     :If 0=≢list←TC.F.ListFiles home,'Maintenance/*.aplf'
         r←'There are no Tatin maintenance files'
     :Else
         r←⊂(⎕PW-2)⍴'-'
         r,←⊂'>>> Maintenance files in ',home
         :For filename :In list
             body←1↓⊃TC.F.NGET filename 1
             r,←⊂(⎕PW-2)⍴'-'
             r,←⊂'*** ',⊃,/1↓⎕NPARTS filename
             body←(';'≠⊃¨TC.A.DLB body)/body
             body←(+/∧\'⍝'=⊃¨body)↑body
             r,←2↓¨body
         :EndFor
         r←⍪r
     :EndIf
 :Else
     'You must specify a path'Assert 0≢path
     :If ∨/'[myucmds]'⍷path
         path←TC.GetMyUCMDsFolder'/',{⍵↓⍨⍵⍳']'}path
     :EndIf
     ('Is not a folder: ',path)Assert TC.F.IsDir path
     :If 0=≢list←TC.F.ListFiles home,'Maintenance/*.aplf'
         r←'No maintenance files found in ',home
     :Else
         list←⌽list
         :If 0<≢ind←'Which maintenance functions would you like to execute?' 1 0 TC.CommTools.Select{2⊃⎕NPARTS ⍵}¨list
             list←list[ind]
             :If 0=≢r←TC.Maintenance path dry list
                 r←'No packages found to be changed'
             :EndIf
         :Else
             r←'No maintenance function(s) selected'
         :EndIf
     :EndIf
 :EndIf
