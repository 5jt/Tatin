 reg←{userSettings}EstablishRegistry packageID;registries;uri;alias;list;msg;CR;i
⍝ The purpose of this function is to establish the registry a package can be found in.
⍝ Useful in case the user specified just a package ID.
⍝ Note that `packageID` might be a full ({group}-{name}-{version}) or a partial one
⍝ ({group}-{name} or {group}-{name}-{major}) or even just {name}.
⍝ The function scans all registries according to the user settings and stops as soon
⍝ as it finds a Registry that hosts the package in question.\\
⍝ In case of no hit anywhere an error is thrown.
 reg←registries←''
 :If '.zip'≢⎕C ¯4↑packageID
 :AndIf ~∨/'/\:'∊packageID
     packageID←RemoveAlias packageID
     userSettings←{0<⎕NC ⍵:⍎⍵ ⋄ MyUserSettings}'userSettings'
     registries←userSettings.registries
     'No Registries defined in the Client config file'Assert 1≤≢registries
     registries←(0<registries.priority)/registries
     'No Registries defined in the Client config file that can be scanned'Assert 1≤≢registries
     i←0
     :Repeat
         i+←1
         (uri alias)←i⊃registries.(uri alias)
         :If Reg.IsHTTP uri
             :If 0=Ping uri
                 345 ⎕SIGNAL⍨'Did not respond: ',uri
             :EndIf
             :If 0=≢list←ListPackages_ uri,packageID
                 :Continue
             :Else
                 reg←uri
             :EndIf
         :ElseIf Reg.IsValidPackageID_Complete packageID
             :If F.IsDir(RemoveFileProtocol uri),'/',packageID
                 reg←uri
             :EndIf
         :ElseIf Reg.IsValidPackageName packageID
             :If 0<≢list←F.ListDirs(RemoveFileProtocol uri),'/*-',packageID,'*'
                 list←∪{⍵↑⍨¯1+⍵⍳'-'}¨GetPackageIDFromFilename¨list
                 ('Package name found in ',(⍕≢list),' groups')Assert 1=≢list
                 reg←uri
             :EndIf
         :Else
             :If 0<≢list←F.ListDirs(RemoveFileProtocol uri),'/',packageID,'*'
                 reg←uri
             :EndIf
         :EndIf
     :Until (i=≢registries)∨0<≢reg
     :If 0=≢reg
         CR←⎕UCS 13
         msg←'Package:',CR,'  ',packageID,CR,'not found in any of these Registries:'
         msg,←⊃,/(⊂CR,'  '),¨{0<≢2⊃⍵:'[',(2⊃⍵),']' ⋄ 1⊃⍵}¨registries.(uri alias)
         msg Assert 0
     :EndIf
 :EndIf
⍝Done
