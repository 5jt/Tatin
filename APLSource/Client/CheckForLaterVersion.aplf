 version←{majorVersions}CheckForLaterVersion path;buildFilename;topLevel;buildList
⍝ Checks whether there are later versions available for what's specified by `path`.\\
⍝ Requires to find a Tatin build list in `path`, otherwise it throws an error.\\
⍝ By default only minor and patch number are part of the check. By specifying 1 as left argument
⍝ you may change this default behaviour and include any better major versions.
⍝ This is used only in the user command since Tatin considers packages with different major
⍝ numbers to be different packages, and for good reasons.
 majorVersions←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'majorVersions'
 path←Reg.AddSlash path
 buildFilename←path,Reg.BuildListFilename
 ('No file "',Reg.BuildListFilename,'" found in ',path)Assert F.IsFile buildFilename
 buildList←Reg.JSON⊃F.NGET buildFilename
 topLevel←1=buildList.depth
 version←majorVersions CheckForLaterVersion_ topLevel/buildList.url
 ⍝Done
