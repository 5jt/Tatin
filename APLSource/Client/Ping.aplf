 bool←{ts}Ping uri_;client;request;uri;buff;qdmx;b
⍝ Establish whether the host is up and running with very little overhead.
⍝ `ts` is only useful for test cases
 ts←{0<⎕NC ⍵:⍎⍵ ⋄ (100000 100 100⊥3↑⎕TS)}'ts'
 :If ∧/'[]'∊uri_
     uri←ReplaceRegistryAlias uri_
 :Else
     uri←uri_
 :EndIf
 :If 0=⎕NC'∆KNOWN_SERVERS'
     ∆KNOWN_SERVERS←0 2⍴⍬                       ⍝ Used to remember the servers that got successfuly pinged
 :EndIf
 :If 0<≢∆KNOWN_SERVERS
 :AndIf ~∧/b←∆KNOWN_SERVERS[;2]=ts
     ∆KNOWN_SERVERS←b⌿∆KNOWN_SERVERS            ⍝ Remove everything older than today
 :EndIf
 :If (⊂uri)∊∆KNOWN_SERVERS[;1]
     bool←1
 :Else
     bool←0
     request←R.NewRequest 0
     request.RequestTarget←'/ping'
     client←NewClient uri
     client.ClientTimeout←4
     :Trap 345
         buff←client R.SendAndReceive request
         bool←1
         ∆KNOWN_SERVERS⍪←uri ts
     :Else
     ⍝ Just for debugging
     :EndTrap
 :EndIf
⍝Done
