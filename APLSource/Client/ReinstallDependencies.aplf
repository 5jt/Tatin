 r←{noBetas}ReInstallDependencies y;installFolder;registry;deps;refs;noBetas
⍝ Takes a folder that hosts a file apl-dependencies.txt as mandatory argument.\\
⍝ The file apl-buildlist.json as well as all directories in that folder will be deleted.
⍝ Then all packages listed in the file apl-dependencies.txt are re-installed from scratch.\\
⍝ Note that a package with a higher major version number is considered a different package.\\
⍝ By default all known Registries with a priority greater than 0 are scanned, but you may
⍝ specify a particular Registry as a second (optional) argument.\\
⍝ By default beta versions are considered. You may change this by specifying a 1 as left argument.
 r←''
 noBetas←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'noBetas'
 y←⊆y
 (installFolder registry)←2↑y,(≢y)↓'' ''
 'Mandatory argument (install directory) must not be empty'Assert 0<≢installFolder
 installFolder←'apl-dependencies.txt'{⍵↓⍨(-≢⍺)×⍺≡⎕C(-≢⍺)↑⍵}installFolder
 'Not a directory'Assert F.IsDir installFolder
 'Directory does not host a file apl-dependencies.txt'Assert F.IsFile installFolder,'/apl-dependencies.txt'
 deps←⊃F.NGET(installFolder,'/apl-dependencies.txt')1
 'Dependency file is empty'Assert 0<≢deps
 F.DeleteFile installFolder∘,¨({'/',⊃,/1↓⎕NPARTS ⍵}¨F.ListFiles installFolder,'/')~⊂'/apl-dependencies.txt'
 F.RmDir F.ListDirs installFolder,'/'
 PrintToSession(⎕PW-1){⍺↑'--- ',⍵,' ',⍺⍴'-'}'Re-installing packages in ',installFolder,' with',(noBetas/'out'),' any beta versions'
 refs←ReInstallDependencies_ deps installFolder registry noBetas
⍝Done
