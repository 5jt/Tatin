 list←{parms}ListPackages_ uri;client;request;res;host;packageID
 :If 0=⎕NC'parms'
     parms←⎕NS''
     parms.(group tags)←⊂''
 :EndIf
 (host packageID)←Reg.SeparateUriAndPackageID uri
 packageID←{0=≢⍵:'*' ⋄ ⍵}packageID
 client←NewClient host
 request←R.NewRequest 0
 request.URI←'/v1/packages/',{⍵↓⍨'/'=1⍴⍵}packageID
 :If 0<≢⊃,/parms.(group tags)
     request.URI,←'?'
     :If 0<≢⊃,/parms.group
         request.URI,←'group=',parms.group
         request.URI,←(0<≢⊃,/parms.tags)/'&'
     :EndIf
     :If 0<≢⊃,/parms.tags
         request.URI,←'tags=',parms.tags
     :EndIf
 :EndIf
 request.Headers,←⊂'Accept' 'application/json'
 res←client R.SendAndReceive request
 ('Requesting files failed with status code ',⍕res.StatusCode)Assert 200=res.StatusCode
 list←{(,0)≡⍴⍵:'' ⋄ ⎕JSON⍠('Dialect' 'JSON5')⊣⍵}⊣res.Content
⍝Done
