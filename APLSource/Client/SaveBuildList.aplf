 {r}←{append}SaveBuildList(buildList path);filename;ns;bool
⍝ Takes a dependency tree (`buildlist`) and (by default) appends it to `path`.\\
⍝ The optional left arguemtn `append` defaults to 1, meaning that `buildlist` is
⍝ appended to an already existing build list. If set to 0 any existing file is replaced.
 r←⍬
 append←{0<⎕NC ⍵:⍎⍵ ⋄ 1}'append'
 buildList[;3]←RemoveFileProtocol¨buildList[;3]
 filename←path,'/',Reg.BuildListFilename
 :If F.IsFile filename
 :AndIf append
     ns←⎕JSON⍠('Dialect' 'JSON5')⊣⊃F.NGET filename
     bool←~ns.packageID∊buildList[;3]
     ns.(depth packageID url)←bool∘/¨ns.(depth packageID url)
 :Else
     ns←⎕NS''
     ns.(depth packageID url)←⊂''
 :EndIf
 :If ~append
     F.DeleteFile path
 :EndIf
 ns.depth,←buildList[;1]
 ns.packageID,←buildList[;2]
 ns.url,←buildList[;3]
 (⊂Reg.JSON ns)F.NPUT filename 1
⍝Done
