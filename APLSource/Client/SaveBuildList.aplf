 {buildList}←{append}SaveBuildList(buildList path);filename;ns;bool;b;groupNameMajor;topLevel;mat
⍝ Takes a dependency tree (`buildlist`) and saves it into `path`.\\
⍝ The optional left argument `append` defaults to 1, meaning that `buildlist` is
⍝ appended to an already existing build list. If set to 0 an existing build list is replaced.
 append←{0<⎕NC ⍵:⍎⍵ ⋄ 1}'append'
 buildList[;3]←RemoveFileProtocol¨buildList[;3]
 filename←path,Reg.BuildListFilename{('/',⍺)/⍨⍺≢(-≢⍺)↑⍵}path
 :If F.IsFile filename
 :AndIf append
     ns←⎕JSON⍠('Dialect' 'JSON5')⊣⊃F.NGET filename
 :Else
     ns←⎕NS''
     ns.(depth packageID url)←⊂''
 :EndIf
 ns.depth,←buildList[;1]
 ns.packageID,←buildList[;2]
 ns.url,←buildList[;3]
 ns.url←{'/'@(⍸'\'=⍵)⊢⍵}¨ns.url
⍝ mat←TakeBestVersion1 ns           ⍝ This seems to be not a correct implementation of minimum version selection
 mat←TakeBestVersion2 ns
 ns.(depth packageID url)←↓⍉mat
 (⊂Reg.JSON ns)F.NPUT filename 1
 buildList←mat
⍝Done
