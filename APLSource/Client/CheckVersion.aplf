 identifier←CheckVersion identifier;packageID;path;CR;msg
 packageID←GetPackageIDFromFilename identifier
 :If ~Reg.IsValidPackageID_Complete packageID
 :AndIf '.zip'≢⎕C ¯4↑identifier
     :If ~∨/'\/'∊identifier
         CR←⎕UCS 13
         msg←'You must specify a Registry for this package ID:',CR
         msg,←'  ',packageID,CR
         msg,←'because it does not carry a full version number (major+minor+patch)'
         msg Assert 0
     :EndIf
     path←(-≢packageID)↓identifier
     identifier←GetBestVersionNumber packageID path
     :If ~Reg.IsHTTP path
         ('Not found: ',packageID)Assert 0<≢identifier
         ('Not found: ',packageID)Assert F.IsDir identifier
     :EndIf
 :EndIf
