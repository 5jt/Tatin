 {r}←(fn Maintenance_)(path dry details confirmation);folders;filename;data;json;data2;pkgName;zipFilename;zip;tempFolder;zipData;carryOut;buff
 r←''
 :If '.git'≢2⊃⎕NPARTS path,'/'
     filename←path,'/apl-package.json'
     :If ⎕NEXISTS filename
         data←⎕JSON⍠('Dialect' 'JSON5')⊢⊃F.NGET filename
         data2←⎕NS data
         data2←fn data2
         json←Reg.JSON data2
         :If ≢/Reg.JSON¨data2 data
             :If dry
                 ⎕←'Would be changed: ',filename,CR
                 :If details
                     buff←data Diff data2
                     ⎕←CR ProcessDiff buff
                 :EndIf
             :Else
                 :If confirmation
                     :If details
                         ⎕←'*** ',filename
                         buff←data Diff data2
                         ⎕←CR ProcessDiff buff
                     :EndIf
                     :If ~carryOut←1 CommTools.YesOrNo'MaintenenaceConfirmation@Perform change?'
                         ⎕←'Skipped by user: ',filename
                     :EndIf
                 :Else
                     carryOut←1
                 :EndIf
                 :If carryOut
                     (⊂json)F.NPUT filename 1
                     ⎕←'Changed: ',filename,CR
                 :EndIf
             :EndIf
         :EndIf
         pkgName←data2.(group,'-',name,'-',{⍵↑⍨¯1+⍵⍳'+'}version)
         zipFilename←path,'/',pkgName,'.zip'
         :If ⎕NEXISTS zipFilename
             zip←⎕NEW ##.ZipArchive(,⊂zipFilename)
             :If (⊂'apl-package.json')∊zip.List
                 tempFolder←F.GetTempSubDir''
                 Assert'apl-package.json'zip.ExtractTo tempFolder
                 zipData←Reg.JSON⊃F.NGET tempFolder,'/apl-package.json'
                 :If json≢Reg.JSON zipData
                     :If dry
                         ⎕←'Would be changed: ',zipFilename,CR
                         :If details
                             data2←Reg.JSON json
                             buff←zipData Diff data2
                             ⎕←CR ProcessDiff buff
                         :EndIf
                     :Else
                         :If confirmation
                             :If details
                                 ⎕←'*** ',filename
                                 data2←Reg.JSON json
                                 buff←zipData Diff data2
                                 ⎕←CR ProcessDiff buff
                             :EndIf
                             :If ~carryOut←1 CommTools.YesOrNo'MaintenenaceConfirmationZip@Perform change?'
                                 ⎕←'Skipped by user: ',filename
                             :EndIf
                         :Else
                             carryOut←1
                         :EndIf
                         :If carryOut
                             zip.Delete'apl-package.json'
                             path zip.Add'apl-package.json'
                             ⎕←'Changed: ',zipFilename,CR
                         :EndIf
                     :EndIf
                 :EndIf
                 {}F.RmDirByForce tempFolder
             :EndIf
             zip.Dispose
         :EndIf
     :EndIf
 :EndIf
 :If 0<≢folders←(F.ListDirs path,'/')~⊂'.git'
     fn{(⍺⍺ Maintenance_)⍵}¨(⊂¨folders),¨dry,¨details,¨confirmation
 :EndIf
 ⍝Done
