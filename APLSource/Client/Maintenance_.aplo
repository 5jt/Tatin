 r←(fn Maintenance_)(path dry);folders;filename;data;json;data2;pkgName;zipFilename;zip
 r←''
 :If '.git'≢2⊃⎕NPARTS path,'/'
 :AndIf ~⎕NEXISTS path,'/apl-buildlist.json'
     filename←path,'/apl-package.json'
     :If ⎕NEXISTS filename
         data←⎕JSON⍠('Dialect' 'JSON5')⊢⊃F.NGET filename
         data2←⎕NS data
         data2←fn data2
         :If ≢/Reg.JSON¨data2 data
             :If dry
                 r,←'Would be changed: ',filename,⎕UCS 13
             :Else
                 json←Reg.JSON data2
                 (⊂json)F.NPUT filename 1
                 r,←'Changed: ',filename,⎕UCS 13
             :EndIf
             pkgName←⊃,/1↓⎕NPARTS ¯1↓⊃⎕NPARTS filename
             zipFilename←path,'/',pkgName,'.zip'
             :If ⎕NEXISTS zipFilename
                 zip←⎕NEW ##.ZipArchive(,⊂zipFilename)
                 :If (⊂'apl-package.json')∊zip.List
                     :If dry
                         r,←'Would be changed: ',zipFilename,⎕UCS 13
                     :Else
                         zip.Delete'apl-package.json'
                         path zip.Add'apl-package.json'
                         r,←'Changed: ',zipFilename,⎕UCS 13
                     :EndIf
                 :EndIf
                 zip.Dispose
             :EndIf
         :EndIf
     :Else
         :If 0<≢folders←(F.ListDirs path,'/')~⊂'.git'
             r,←∊fn{(⍺⍺ Maintenance_)⍵}¨(⊂¨folders),¨dry
         :EndIf
     :EndIf
 :EndIf
 ⍝Done
