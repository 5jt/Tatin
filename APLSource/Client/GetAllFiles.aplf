 list←home GetAllFiles cfg;list;subList;default;extension
⍝ ⍵ Is an object presenting a package's config file.\\
⍝ ⍺ Is the folder that hosts the package.\\
⍝ ← Is a list of all files within cfg.source.\\
⍝ This function has a side effect: if "source" is not found on disk at all, it is created as a folder.
⍝ This might happen when "source" will be exclusively used for dependencies.
 :If F.IsFile home,cfg.source
     list←home∘,¨⊆cfg.source
 :Else
     home,←(~(¯1↑home)∊'\/')/'/'
     :If 0=F.IsDir home,cfg.source
         'Create!'F.CheckPath home,cfg.source
         list←''
         :Return
     :Else
         list←⊃('recursive' 1)F.Dir home,cfg.source,'/'
     :EndIf
 :EndIf
 list/⍨←F.IsFile list
 list←(≢home)↓¨list
 list~←Reg.DependenciesFilename CFG_Name
 extension←'.aplf' '.aplo' '.aplc' '.apln' '.dyalog' '.apla'
 list/⍨←(3⊃∘⎕NPARTS¨list)∊extension
⍝Done
