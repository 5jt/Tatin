 {r}←CloseConnections force;list;flag;qdmx
⍝ Close all permanent connections defined within ∆PermanentConnections, and delete that namespace.
⍝ That can be prevented by injecting `keepOpen←1` into ∆PermanentConnections which in turn
⍝ can be overwritten with ⍵←1.
 r←⍬
 :If 9=⎕NC'∆PermanentConnections'
     :If 0<≢∆PermanentConnections.⎕NL 9
         flag←1
         :If ~force
             :If 0<∆PermanentConnections.⎕NC'keepOpen'
             :AndIf ∆PermanentConnections.keepOpen
                 flag←0
             :EndIf
         :EndIf
         :If flag
             list←∆PermanentConnections.{⍎¨↓⎕NL 9}⍬
             :Trap 0
                 {}{⍵ R.Close 0}¨list
             :Case 1006
                 'Conga DLL reports an error 1006 and won''t work anymore - restart APL'⎕SIGNAL 11
             :Else
                 qdmx←⎕DMX
                 :If ∨/'DLL Error: 1006'⍷qdmx.EM   ⍝ This may come with ⎕EN=999
                     'Conga DLL reports an error 1006 and won''t work anymore - restart APL'⎕SIGNAL 11
                 :Else
                 ⍝ This happens, but very rarely.
                 ⍝ We need to find out why
                     ⎕TRAP←0 'S'
                     ∘∘∘
                 :EndIf
             :EndTrap
             ⎕EX'∆PermanentConnections'
         :EndIf
     :Else
         ⎕EX'∆PermanentConnections'
     :EndIf
 :EndIf
