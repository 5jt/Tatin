 {r}←LoadDependencies(installFolder targetSpace);path;tree;dep2;saveIn;cfg;msg;packageID;saveIn2;ref;url;level;refs
⍝ Takes a folder with a build list, typically the result of installing packages.
⍝ Loads all packages according to that list and injects required references into `targetSpace`.
⍝ Returns a vector with references to the loaded packages (no dependencies).
⍝ Might be empty if the package was already loaded, for example as a dependency.
 r←refs←⍬
 :If 0=⎕NC⊂⍕targetSpace
     targetSpace ⎕NS''
 :EndIf
 CheckBuildList installFolder
 :If 0<≢tree←LoadBuildList installFolder
     :For level packageID url :In ↓tree
         path←installFolder,'/',packageID
         ('Cannot find package "',path,'"')Assert F.Exists path
         saveIn←GetTatinRootSpace targetSpace
         cfg←ReadPackageConfigFile path
         packageID←1 GetPackageIDFromFilename path
         saveIn,←'.',packageID
         :If 0=⎕NC saveIn   ⍝ Might already exist
             msg←Reg.ImportWithLink saveIn(path,'/',cfg.source)
             'Link failed to import code'Assert'Imported:'{⍺≡(≢⍺)↑⍵}msg
             ref←⍎saveIn
             EstablishConstants ref cfg path
             refs,←ref
             :If 1=level
                 r,←ref
             :EndIf
         :EndIf
         :If 1=level
             cfg LinkTargetToPackage saveIn targetSpace
         :EndIf
     :EndFor
     LinkDependencies refs
 :EndIf
⍝Done
