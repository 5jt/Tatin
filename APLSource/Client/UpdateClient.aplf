 version←UpdateClient(tag targetFolder);data;folder;tempFile;tno;run;targetStUp;atLeast19
⍝ Updates the client to the version indicated by `tag`, and returns the version number of the new version.\\
 'Updating Tatin requires at least 20 MB free memory'Assert 20<⌊⎕WA÷1024×1024
 version←''
 data←GetContentFromGitHub'/aplteam/Tatin/releases/download/',tag,'/Tatin-Client-',(1↓tag),'.zip'
 folder←F.GetTempSubDir'Tatin-Upgrade'
 tempFile←folder,'/',tag,'.zip'
 tno←tempFile ⎕NCREATE 0
 data.Content ⎕NAPPEND tno 80
 ⎕NUNTIE tno
 tempFile ZipArchive.UnzipTo folder
 ⎕NDELETE tempFile

⍝ Catch the "Run" function for 19.0 or later
 :If atLeast19←19≤⊃(//)⎕VFI{⍵/⍨2>+\⍵='.'}2⊃# ⎕WG'APLVersion'
     data←GetContentFromGitHub'/aplteam/Tatin/releases/download/',tag,'/Run.aplf'
     run←'UTF-8'⎕UCS ⎕UCS data.Content
     (⊂run)⎕NPUT folder,'/Run.aplf'
 :EndIf

⍝ Move the new version from temp to the target folder
 3 ⎕MKDIR targetFolder
 (targetFolder,'/..')⎕NCOPY⍠('IfExists' 'Replace')⊣folder,'/Tatin'
 :If atLeast19
     targetStUp←targetFolder,'/../../../StartupSession/CiderTatin/'
     3 ⎕MKDIR targetStUp
     targetStUp ⎕NCOPY⍠('IfExists' 'Replace')⊣folder,'/Run.aplf'
 :EndIf

 version←{⍵↓⍨'v'=1⍴⍵}tag
⍝Done
