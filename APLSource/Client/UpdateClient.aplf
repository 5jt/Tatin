 (version ref target)←{tag}UpdateClient y;force;GH;request;client;data;data2;redirect;tempFile;tno;folder;wspath;msg;flag;rc;flag2
⍝ Updates the client to (by default) the latest version, and returns the version number of the new version.\\
⍝ A Boolean right argument (`force`) is mandatory, but one might specify also a folder Tatin is installed in.
⍝ The user command will do this automatically with the folder the user command was loaded from.\\
⍝ Asks for confirmation from the user unless `force` is 1.\\
⍝ If you want to update to a specific version you may specify the desired version in ⍺.
⍝ The format must be something like "v1.0.2".
 (force target)←2↑(⊆y),⊂''
 'Right argument must be a Boolean'Assert(⊂force)∊0 1
 'Updating Tatin requires at least 20 MB WSSIZE'Assert 20<⌊⎕WA÷1024×1024
 version←ref←target←''
 :If flag←0=⎕NC'tag'
     GH←⎕NEW ##.GitHubAPIv3(,⊂'aplteam')
     tag←GH.GetLatestTag'Tatin'
 :Else
     'Invalid version number'Assert'v'=1⍴tag
 :EndIf
 :If (1↓tag)≡{⍵↑⍨¯1+⍵⍳'+'}2⊃Reg.Version
     ⎕←'Tatin is up-to-date: ',⊃{⍺,' from ',⍵}/1↓Reg.Version
 :Else
     msg←'You version of Tatin is ',2⊃Reg.Version
     msg,←(⎕UCS 13),'Latest published version is ',1↓tag
     msg,←(⎕UCS 13),'Would you like to download and install ',(1↓tag),' ?'
     :If ~flag ⍝ Then the user has requested a particular version, and we assume she knows what she's doing
     :OrIf msg{⍵:⍵ ⋄ YesOrNo ⍺}force
         request←R.NewRequest 0
         request.RequestTarget←'/aplteam/Tatin/releases/download/',tag,'/Tatin-Client-',(1↓tag),'.zip'
         client←R.NewClient 0
         client.Host←'github.com'
         request.Headers,←⊂'User-Agent' 'aplteam'
         data←client R.SendAndReceive request
         'Invalid HTTP status code'Assert 302≡data.StatusCode
         redirect←2⊃'location'{⍵⊃⍨(⊃¨⍵)⍳⊂⍺}data.Headers
         redirect←'https://'{l←≢⍺ ⋄ ⍵↓⍨l×⍺≡l↑⍵}redirect
         client←R.NewClient 0
         (client.Host request.RequestTarget)←{l←+/1>+\'/'=⍵ ⋄ (l↑⍵)(l↓⍵)}redirect
         data2←client R.SendAndReceive request
         folder←F.GetTempSubDir'Tatin-Upgrade'
         tempFile←folder,'/',tag,'.zip'
         tno←tempFile ⎕NCREATE 0
         data2.Content ⎕NAPPEND tno 80
         ⎕NUNTIE tno
         data2.Content←''
         tempFile ZipArchive.UnzipTo folder
         ⎕NDELETE tempFile
         :If 0=≢target
             target←GetMyUCMDsFolder'Tatin'
         :EndIf
         flag2←0
         :Repeat
             (rc msg)←F.RmDirByForce target
             :If rc=0
                 flag2←1
             :ElseIf 0=YesOrNo('Removing >',target,'> failed with rc=',⍕rc)('Error message: ',⍕∊msg)'Try again?'
                 '*** Updating Tatin cancelled'⎕SIGNAL 11
             :Else
                 ⎕DL 1
             :EndIf
         :Until flag2
         (⊃⎕NPARTS target)⎕NCOPY folder,'/Tatin'
         version←{⍵↓⍨'v'=1⍴⍵}tag
         ref←⎕SE.⎕NS''

         :If 0
             ⍝ It appears that overwriting Conga in the current WS does not really work, no matter what.
             ⍝ For that reason this is not active.
             :Trap 11
             ⍝ We try to copy a function from the new version that can execute the remaining steps.
             ⍝ That way we are more flexible. The ⎕CY is trapped only because in the early stages (after UpdateClient was introduced)
             ⍝ there might be no such function yet; then the required steps are executed by this fn in :Else
             ⍝ Note however that the function must not be executed from within UpdateClient because that would
             ⍝ prevent the namespace ⎕SE.Tatin from disappearing causing potentially a Conga problem: DLL error 1006
                 '#._Tatin.Client.UpdateClient_Continue'ref.⎕CY folder,'/Tatin/Client.dws'
             :Else
                 ref.⎕FX ⎕CR'UpdateClient_Continue'
             :EndTrap
         :EndIf

     :EndIf
 :EndIf
⍝Done
