 list←{dateFlag}ListVersions_ uri;request;res;host;packageID
 dateFlag←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'dateFlag'
 (host packageID)←Reg.SeparateUriAndPackageID uri
 packageID←{0=≢⍵:'*' ⋄ ⍵}packageID
 request←R.NewRequest 0
 request.URI←'/v1/packages/versions/',packageID
 :If dateFlag
     request.URI,←'?publishingdate=1'
 :EndIf
 request.Headers,←⊂'Accept' 'application/json'
 res←host SendAndReceive request
 :If 200≠res.StatusCode
     :If 0<≢res.Content
         ('Server: ',res.Content)Assert 0
     :Else
         ('Failed with status code ',⍕res.StatusCode)Assert 0
     :EndIf
 :EndIf
 list←⎕JSON⍠('Dialect' 'JSON5')⊣res.Content
 list←↑⍣dateFlag⊣list
⍝Done
