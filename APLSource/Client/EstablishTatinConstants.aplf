 {r}←EstablishTatinConstants(ref cfg folder);deps;ref2;res;msg;make
⍝ We establish a couple of variables in code.TatinVars for convinience
 r←⍬
 ref2←⍎'TatinVars'ref.code.⎕NS''
 make←{
     ' '=1↑0⍴⍵:'' 1
     msg←'Fixing Tatin''s "',⍺,'" function failed; check line(s): ',' '~⍨⍕⊃{⍺,',',⍵}/⍵-1
     msg 0}
 res←ref2.⎕FX'r←HOME'('r←''',folder,'''')   ⍝ We might change this at a later stage if the packages does not have any assets
 Assert/'HOME'make res
 res←ref2.⎕FX'r←ASSETS'('r←''',cfg.assets,'''')
 Assert/'HOME'make res
 res←ref2.⎕FX'r←CONFIG'('r←''',(Reg.JSON cfg),'''')
 Assert/'CONFIG'make res
 res←ref2.⎕FX'r←ID'('r←''',(⊃{⍺,'-',⍵}/cfg.(group name version)),'''')
 Assert/'ID'make res
 res←ref2.⎕FX'r←URI'('r←''',({0<⍵.⎕NC'uri':⍵⍎'uri' ⋄ ''}cfg),'''') ⍝ "uri" is set by server but not for local installations
 Assert/'URI'make res
 :If 0<≢deps←Dependencies.Get folder
     res←ref2.⎕FX'r←DEPENDENCIES'('r←,⊆',(⊃{⍺,' ',⍵}/'''',¨deps,¨''''),'')
     Assert/'DEPENDENCIES'make res
 :Else
     res←ref2.⎕FX'r←DEPENDENCIES' 'r←⍬'
     Assert/'DEPENDENCIES'make res
 :EndIf
⍝Done
