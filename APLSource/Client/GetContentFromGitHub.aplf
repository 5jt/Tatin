 data←{group}GetContentFromGitHub url;request;client;data;redirect;data2
⍝ Returns the content of a given url from GitHub.
⍝ `url` is typically something like:
⍝ `/<group>/<name>/releases/download/<version-tag/<zip-name>-<tag>.zip`
⍝ `group` is optional and defaults to "aplteam".
⍝ Handles 302 ("page redirected") automatically.
 :If 0=⎕NC'group'
     group←'aplteam'
 :EndIf
 request←R.NewRequest 0
 request.RequestTarget←url
 client←R.NewClient 0
 client.Host←'github.com'
 request.Headers,←⊂'User-Agent'group
 data←client R.SendAndReceive request
 :If data.StatusCode=302
     redirect←2⊃'location'{⍵⊃⍨(⊃¨⍵)⍳⊂⍺}data.Headers
     redirect←'https://'{l←≢⍺ ⋄ ⍵↓⍨l×⍺≡l↑⍵}redirect
     client←R.NewClient 0
     (client.Host request.RequestTarget)←{l←+/1>+\'/'=⍵ ⋄ (l↑⍵)(l↓⍵)}redirect
     data←client R.SendAndReceive request
     ('Accessing ',request.RequestTarget,' failed!',(⎕UCS 13),'HTTP status code is ',(⍕data.StatusCode),' rather than the expected 200')Assert data.StatusCode≡200
 :Else
     ('Accessing ',request.RequestTarget,' failed!',(⎕UCS 13),'HTTP status code is ',(⍕data.StatusCode),' rather than the expected 302')Assert data.StatusCode≡200
 :EndIf
