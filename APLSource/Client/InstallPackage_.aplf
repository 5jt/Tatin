 tempDir←InstallPackage_(packageID url);zipFilename;isHTTP
⍝ This function fetches a package and unzips it into a temp folder.
⍝ It is called by both `InstallPackage` and `LoadPackage`.
 :If isHTTP←Reg.IsHTTP url
     PrintToSession'  Fetching ',url,'...'
     zipFilename←GetPackageViaHTTP url
 :Else
     :If '.zip'≡¯4↑url
         zipFilename←RemoveFileProtocol RemoveFileProtocol url
     :Else
         zipFilename←RemoveFileProtocol(RemoveFileProtocol url),'/',packageID,'.zip'
     :EndIf
 :EndIf
 tempDir←Reg.GetTempDir,'/',packageID,'_',GetMD5 zipFilename
 F.RmDir tempDir
 ⎕DL 0.02
 ('Does already exist: ',tempDir)Assert 0=F.IsDir tempDir
 F.MkDir tempDir
 PrintToSession'  Unzipping ',zipFilename,'...'
 zipFilename DotNetZip.UnzipTo tempDir
 tempDir Add2Config'uri'(RemoveFileProtocol url)
 :If isHTTP
     F.DeleteFile zipFilename   ⍝ Then it is a temporay zipfile
 :EndIf
⍝Done
