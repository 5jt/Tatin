 (rc report)←ClearCache url;path;domains;domains;rc_;more;en;domains2;this2;rc2;en2;more2;domain
⍝ Clears the Tatin package cache of `url`. If `url` is an empty vector everything is deleted.
⍝ Returns a two-element vector:
⍝ [1] A 0 in case of success and 1 otherwise
⍝ [2] A simple char vector, possibly with injected LFs (⎕UCS 10), reporting failures. Is empty in case `rc` is 0.
⍝ Because somebody/something else might just be in the process of saving a package in the cache the operation might well fail.
⍝ For that reason it attempts to remove one package after the other. The function `FilesAndDirs.RmDir` tries
⍝ multiple times before giving up.
⍝ Any errors during RmDir are suppressed but reflected by the result.
 rc←0 ⋄ report←''
 path←GetPathToPackageCache
 :If 0<≢domains←F.ListDirs path,'/*'
     domains←((⊂'temp')≢¨{2⊃⎕NPARTS ⍵}¨domains)/domains
     :If 0<≢url
         domains←(url∘≡¨{⊃,/1↓⎕NPARTS ⍵}¨domains)/domains
     :EndIf
 :AndIf 0<≢domains
     :For domain :In domains
         (rc_ en more)←F.RmDir domain
         :If 0≠rc_
             rc←1
             report,←(domain,': ',more),⎕UCS 10
             :If 0<≢domains2←F.ListDirs domain,'/*'
                 :For this2 :In domains2
                     (rc2 en2 more2)←F.RmDir this2
                     :If 0≠rc2
                         report,←(this2,': ',more2),⎕UCS 10
                     :EndIf
                 :EndFor
             :EndIf
         :EndIf
     :EndFor
 :EndIf
 ⍝Done
