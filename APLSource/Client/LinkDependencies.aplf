 {r}←LinkDependencies refs;ref;deps;root;destinations;bool;depName;destination;cfgs;cfg;list
⍝ Takes a list of refs, typically returned by `LoadPackage`, and establishes any required references
⍝ to dependencies within those refs.
⍝ `api` rules, but if it's empty then `name` is taken instead.
 r←⍬
 root←{GetTatinRootSpace 1⊃⍵}⍣(0<≢refs)⊣refs
 :If 0<≢refs
     PrintToSession'  Establish references to dependencies...'
     :For ref :In refs
         ref←CheckWsPaths ref
         :If 0<≢deps←Dependencies.Get⊃{0=≢⍺:⍵ ⋄ ⍺}/ref.(∆HOME ∆URI)
             deps←ReplaceRegistryAlias¨deps
             destinations←GetPackageIDFromFilename¨deps
             destinations←(⊂root,'.'),¨Reg.EnforceAPL_Name¨destinations
             destinations←CheckWsPaths¨destinations
             cfgs←Reg.JSON¨destinations⍎¨⊂'∆CONFIG'
             :For cfg destination dep :InEach cfgs destinations deps
                 depName←{⍵↑⍨¯1+⍵⍳'-'}{⍵↓⍨⍵⍳'-'}GetPackageIDFromFilename dep
                 :If 0=≢cfg.api
                     :If 1≠≢list←↓⍎destination,'.code.⎕nl 9'
                         depName ref.{⍎'⎕THIS.code.',⍺,'←',⍵}destination,'.code'
                     :Else
                         depName ref.{⍎'⎕THIS.code.',⍺,'←',⍵}destination,'.code.',1⊃list
                     :EndIf
                 :Else
                     depName ref.{⍎'⎕THIS.code.',⍺,'←',⍵}destination,'.code.',cfg.api
                 :EndIf
             :EndFor
         :EndIf
     :EndFor
 :EndIf
⍝Done
