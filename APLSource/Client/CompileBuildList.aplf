 {buildList}←{replace}CompileBuildList folder;depFilename;oldTree;newTree;b;deps2;ind
⍝ We distinguesh here between level-1 packages and other packages (=dependencies).
⍝ The level-1 packages are called top-level packages.
⍝ If the dependency file was modified after the build list was saved:
⍝ * If a top-level package was removed from the dependency file the build list is re-created
⍝ * If a dependency package is missing, re-installation is required and therefore an error thrown\\
 replace←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'replace'
 folder←Reg.AddSlash folder
 depFilename←folder,Reg.DependenciesFilename
 oldTree←LoadBuildList folder
 newTree←0(GetDependencyTree NoPrintToSession)folder
 :If 0=≢⊃newTree[1;2]                                       ⍝ Is it not a package folder but just an install folder?
     newTree←1↓newTree
     newTree[;1]-←1
 :EndIf

 :If 0<≢newTree                                             ⍝ There might be no depen
 :AndIf 0<≢deps2←(1<newTree[;1])⌿newTree
     deps2←Reg.GetBestVersionNumber¨Reg.RemoveVersionNumber¨deps2[;3]
     ind←⍸newTree[;1]>1
     newTree[ind;2]←GetPackageIDFromFilename¨deps2
     newTree[ind;3]←deps2
 :EndIf

 :If 0<≢oldTree←(oldTree[;2]∊newTree[;2])⌿oldTree
 :AndIf 0<+/b←newTree[;2]∊oldTree[;2]
     newTree[⍸b;3]←oldTree[oldTree[;2]⍳b/newTree[;2];3]     ⍝ Preserve original URLs
 :EndIf
 buildList←(~replace)SaveBuildList newTree folder''
⍝Done
