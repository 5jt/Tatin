 {r}←cfg LinkTargetToPackage(source target);alias;name;ref;from;buff
 r←⍬
 alias←cfg.alias{0=≢⍺:⍵ ⋄ ⍺}cfg.name
 name←cfg.name
 ref←⍎source
 :If 0<≢cfg.api
     from←cfg.api
 :Else
     buff←⎕NPARTS cfg.source
     :If buff[3]∊'.aplf' '.aplo'
         from←2⊃buff
     :Else
         from←cfg.name
     :EndIf
 :EndIf
 :If (ref.⎕NC from)∊3 4
     ⍝ If it is a function it must not be niladic!
     ('Cannot load package "',source,'": entry point is niladic function ')Assert 0≠2 1⊃ref.⎕AT from
 :EndIf
 :If 0=⎕NC source,'.',from
 :AndIf 0=|⎕NC⊂source,'.',from
     0 Assert⍨'Cannot establish API in ',source
 :EndIf
 target⍎alias,'←',source,'.',from
⍝Done
