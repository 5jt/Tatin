 {r}←cfg LinkTargetToPackage(source target);alias;name;ref;from;buff
⍝ Establishes references in the target space pointing to the loaded package.\\
⍝ Returns the fully qualified names of what was established in the target space
 alias←cfg.alias{0=≢⍺:⍵ ⋄ ⍺}cfg.name
 name←cfg.name
 ref←⍎source
 :If 0<≢cfg.api
     from←cfg.api
 :Else
     buff←⎕NPARTS cfg.source
     :If buff[3]∊'.aplf' '.aplo'
         from←2⊃buff
     :Else
         from←cfg.name
     :EndIf
 :EndIf
 :If (ref.⎕NC from)∊3 4
     ⍝ If it is a function it must not be niladic!
     ('Cannot load package "',source,'": entry point is niladic function ')Assert 0≠1 2⊃ref.⎕AT from
 :EndIf
 :If 0=⎕NC source,'.',from
 :AndIf 0=|⎕NC⊂source,'.',from
     0 Assert⍨'Cannot establish API in ',source
 :EndIf
 target⍎alias,'←',source,'.',from
 r←targetSpace,'.',alias
⍝Done
