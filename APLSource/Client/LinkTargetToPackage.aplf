 {r}←cfg LinkTargetToPackage(saveIn target);alias;name;ref;api;buff
⍝ Establishes references in the target space pointing to the loaded package.\\
⍝ Returns the fully qualified names of what was established in the target space.
⍝ Note that this can be a namespace or a function etc.
 alias←cfg.alias{0=≢⍺:⍵ ⋄ ⍺}cfg.name
 name←cfg.name
 ref←⍎saveIn
 :If 0<≢cfg.api
     api←cfg.api
 :Else
     buff←⎕NPARTS cfg.source
     :If buff[3]∊'.aplf' '.aplo'
         api←2⊃buff
     :Else
         api←cfg.name
     :EndIf
 :EndIf
 :If (ref.⎕NC api)∊3 4
     ⍝ If it is a function it must not be niladic!
     ('Cannot load package "',saveIn,'": entry point is niladic function ')Assert 0≠1 2⊃ref.⎕AT api
 :EndIf
 :If 0=⎕NC saveIn,'.',api
 :AndIf 0=|⎕NC⊂saveIn,'.',api
     0 Assert⍨'Cannot establish API for <',api,'>; does not exist'
 :EndIf
 target⍎alias,'←',saveIn,'.',api
 r←target,'.',alias
⍝Done
