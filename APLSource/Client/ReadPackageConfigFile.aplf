 cfg←ReadPackageConfigFile path;cfg_file;cfg;buff;Reg
⍝ Takes path to a package and returns the config file for that package.
 Reg←##.Registry
 :If Reg.IsHTTP path
     cfg←ReadPackageConfigFile_ path
 :Else
     cfg_file←path,'/',CFG_Name
     ('Config file missing: ',cfg_file)Assert F.Exists cfg_file
     cfg←InitPackageConfig''
     ⍝ load in actual
     buff←⊃F.NGET cfg_file
     (('\'=buff)/buff)←'/'
     'Odd number of " ?!'Assert 0=2|'"'+.=buff
     {}'cfg'⎕NS ⎕JSON⍠('Dialect' 'JSON5')⊣buff

      ⍝TODO⍝ The following is required only until we've converted both tatin.dev and test.tatin.dev
     :If 2=cfg.⎕NC'project_url'
         cfg.info_url←cfg.project_url
         cfg.⎕EX'project_url'
     :EndIf
 :EndIf
⍝Done
