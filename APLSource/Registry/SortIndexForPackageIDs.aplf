 ind←SortIndexForPackageIDs data;ind1;ind2
⍝ ⍵ must be one of:
⍝ * A list of package IDs or a list of version numbers.
⍝ * A three-column-matrix (additional columns are ignored):
⍝   [;1] packageID (without any beta part) or version numbers
⍝   [;2] the publishing date as a real (YYYYMMDD.hhmmss)
⍝   [;3] the publishing date but for non-betas 99999999 - this col determins the sort sequence
⍝ The versions are sorted with the newest going bottom, the oldest top.\\
⍝ * For non-betas the version number rules
⍝ * For betas the publishing dates rule\\
⍝ Notes:
⍝ * {group}-{name}-9.0.0 comes before {group}-{name}-10.0.0
⍝ * {group}-{name}-9.0.0 20101213.010203 comes before {group}-{name}-9.0.0 20110114.010203
 ind←⍬
 :If 0<≢data
     Assert 0=∧/~'.'∊¨data
     :If 1=⍴⍴data
         data←InjectPublishingDate data
     :EndIf
     ind1←⍒data[;3]
     data←data[ind1;]
     data[;1]←{⊃,/1↓⎕NPARTS ⍵}¨data[;1]
     data[;1]←RemoveBetaPart¨data[;1]
     ind2←SeparateUriAndPackageID_ data[;1]
     ind←ind1[ind2]
 :EndIf
 ⍝Done
