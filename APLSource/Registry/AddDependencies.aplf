 pkgs←registryPath AddDependencies pkgs;pkg;filename;deps;i
⍝ Takes a list of package IDs, established any dependencies and adds them if they are not already on the list
 :If 0<≢pkgs
     :For i :In ⍳≢pkgs
         pkg←1⊃pkgs[i;]
         filename←registryPath,pkg,'/',DependenciesFilename
         :If F.IsFile filename
             deps←⊃⎕NGET filename 1
             :If 2=2⊃⍴pkgs
                 deps←{'@'∊⍵:⍵↓⍨⍵⍳'@' ⋄ ⍵}¨deps         ⍝ Remove package aliase
                 pkgs⍪←((~deps∊pkgs)/deps),[1.5]⊂pkg    ⍝ Only what we don't already have
             :Else
                 pkgs[i;1]←⊂(⊂pkg),(⊂2⍴' '),¨deps
             :EndIf
         :EndIf
     :EndFor
     :If ~2=2⊃⍴pkgs
         pkgs←⍪{⊃,/⍵}⊆¨,pkgs
     :EndIf
 :EndIf
