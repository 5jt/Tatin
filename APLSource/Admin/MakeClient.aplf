 {r}←{type}MakeClient batchFlag;msg;F;targetPath;myucmds;ts1;ts2;path;reg;_userSettings;list;zipFilename;zipContent;fileList;b;rc;more;en;dlls;myZip
⍝ This functions creates a new Tatin Client. It can be called from a developer's session (⍵←0) or as part of an automated "Make"
⍝ process with ⍵←1.\\
⍝ In case of a problem an error is thrown if ⍵=0 and ⎕OFF 123 if ⍵=1.\\
⍝ `type` specifies what part of the version number should be bumped. 0 (the default) means only the build ID, which is always bumped anyway.\\
⍝ 1 means "patch" shall be bumped, 2 for "minor" amnd 3 for "major". Other values result in an error or ⎕OFF 123 respectively.\\
⍝ Note that as part of an automated process this function should be called *before* `MakeServer` because it bumps the
⍝ version number while `MakeServer` does not.
⍝ Version 1.0.0 from 2020-12-02
 r←⍬
 F←##.FilesAndDirs
 type←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'type'
 :If ~batchFlag
     ⎕←(⎕PW-1)⍴'-'
     msg←''
     msg,←⊂'This function will close all acre projects, delete everything that is not needed in order to'
     msg,←⊂'use the Tatin client, re-arrange stuff and finally save the workspace together with'
     msg,←⊂'everything that is needed by the client in Dist/Client/Client.dws'
     msg,←⊂'The Tatin client will be copied from that WS into ⎕SE once a Tatin user command is invoked.'
     msg,←⊂''
     msg,←⊂'It also checks whether the user command should be copied to the MyCMDs folder or vice versa,'
     msg,←⊂'depending on where the last change was made.'
     msg,←⊂''
     msg,←⊂'Eventually all that''s required is zipped into a file <Tatin-Client-{major}.{Minor}.{Patch}.zip'
     msg,←⊂'That file is created within the Dist/ folder.'
     ⎕←⍪msg
     ⎕←''
 :EndIf
 :If batchFlag
 :OrIf 1 YesOrNo'Would you like to continue?'
     batchFlag UpdateVersionNumber type
     {}⎕SE.acre.CloseProject''('all' 1)
     Assert'#._Tatin'≡⎕SE.acre.OpenProject #.Tatin.AcreConfig.ProjectFolder'#._Tatin'
     {}⎕SE.acre.CloseProject''('all' 1)
     #._Tatin.⎕EX¨'Server' 'Todo' 'TestCases' 'Simu' 'TestCasesServer'
     #._Tatin.Admin.⎕EX¨'Initialize_Server' 'RunTestServer' 'CreateAllRefs_Server'
     #._Tatin.Admin.Initialize_Client
     #._Tatin.Admin.⎕EX'Initialize_Client'
     ⎕LX←''
     F.MkDir'Dist'
     targetPath←'Dist/Client'
     F.RmDir targetPath
     ⎕DL 0.1
     F.MkDir targetPath
     ⎕DL 0.1
     ⎕WSID←targetPath,'/Tatin/Client'
     Assert F.MkDir targetPath,'/Tatin'
     #.⎕EX¨(' '~¨⍨↓#.⎕NL⍳16)~⊂'_Tatin'
     0 ⎕SAVE ⎕WSID
     (targetPath,'/')⎕NCOPY⍠('IfExists' 'Replace')⊣'Tatin.dyalog'
     'Create!'F.CheckPath targetPath,'/Tatin/Assets'
     (targetPath,'/Tatin/Assets/')⎕NCOPY⍠('IfExists' 'Replace')⊣'./Assets/Runtime/'
     _userSettings←⎕NEW #._Tatin.Client.UserSettings(,⊂targetPath,'/Tatin/')
     reg←_userSettings.GetRegistry'[tatin]'
     _userSettings.ReplaceRegistry reg
     (targetPath,'/')⎕NCOPY⍠('IfExists' 'Replace')⊣'setup.dyalog.RemoveMe'
     myucmds←#._Tatin.Client.GetMyUCMDsFolder''
     :If 1 YesOrNo'Would you like to copy the Tatin client to ',myucmds
         F.RmDir myucmds,'/Tatin'
         F.MkDir myucmds,'/Tatin'
         (myucmds,'/Tatin/')⎕NCOPY⍠('IfExists' 'Replace')⊣⎕WSID,'.dws'
         :If 0=F.Exists myucmds,'/tatin.dyalog'
             (myucmds,'/')⎕NCOPY⍠('IfExists' 'Replace')⊣targetPath,'/tatin.dyalog'
         :Else
             ts1←' -:,./'~⍨##.APLTreeUtils2.FormatDateTime⊃⊃('type' 3)F.Dir myucmds,'/tatin.dyalog'
             ts2←' -:,./'~⍨##.APLTreeUtils2.FormatDateTime⊃⊃('type' 3)F.Dir'./tatin.dyalog'
             :If ts1≢ts2
                 :Select ⍋ts1 ts2
                 :Case 1 2
                     (myucmds,'/')⎕NCOPY⍠('IfExists' 'Replace')⊣'./tatin.dyalog'
                 :Case 2 1
                     './'⎕NCOPY⍠('IfExists' 'Replace')⊣myucmds,'/tatin.dyalog'
                 :EndSelect
             :EndIf
         :EndIf
         dlls←F.ListFiles'./CongaDlls/*'
         (⊂F.NormalizePath myucmds,'\Tatin\Assets\RunTime\Conga'){19::shy←⍬ ⋄ ⍺ ⎕NCOPY⍠('IfExists' 'Replace')⊣⍵}¨dlls
     :EndIf
     path←#._Tatin.Client.GetUserHomeFolder,'/',#._Tatin.Client.UserSettings.cfg_name
     :If 0=F.IsFile path
     :OrIf 0 YesOrNo'Overwrite "',path,'" ?'
         path ⎕NCOPY⍠('IfExists' 'Replace')⊣_userSettings.path2config
     :EndIf
     F.DeleteFile¨F.Dir'Dist/Tatin-Client-*.zip'
     zipFilename←'Dist/Tatin-Client-',({{⍵/⍨∧\⍵∊'.',⎕D}⍵/⍨3>+\'.'=⍵}(2⊃##.Client.Version),''),'.zip'
     ⎕DL 0.1
     fileList←'recursive'F.ListFiles targetPath,'\'
     fileList←F.EnforceSlash¨(1+≢targetPath)↓¨fileList
     myZip←⎕NEW ##.DotNetZip(,⊂zipFilename)
     targetPath myZip.Add fileList
    ⍝targetPath ##.DotNetZip.ZipFolder zipFilename   ⍝ This causes potentially trouble on Linux and Mac OS because of the \
     ⎕DL 1
     myZip.Dispose
     zipContent←##.DotNetZip.ListZipContents zipFilename
     'Something went wrong with the ZIP file'Assert fileList{(∧/⍺∊⍵)∧(∧/⍵∊⍺)}zipContent
     :If batchFlag
         ⎕OFF
     :Else
         ⎕←'*** Done;  note that the Tatin acre project has been closed'
     :EndIf
 :EndIf
