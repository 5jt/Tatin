 {r}←MakeServer batchFlag;F;targetPath;zipFilename;fileList;zipContent;list;msg;success
 r←⍬
 :If batchFlag
     ⎕SHADOW'Assert'
     Assert←{(,1)≡,⍵:r←1 ⋄ ⎕OFF 123}
 :Else
     ⎕←(⎕PW-1)⍴'-'
     msg←''
     msg,←⊂'This function will close all acre projects, delete everything that is not needed in order to'
     msg,←⊂'run a Tatin server, re-arrange stuff, set ⎕LX and finally save the workspace together'
     msg,←⊂'with everything that is needed by the server in Dist/Server.dws'
     msg,←⊂''
     msg,←⊂'Eventually all that''s required is zipped into a file <Tatin-Server-{major}.{Minor}.{Patch}.zip'
     msg,←⊂'That file is created within the Dist/ folder.'
     ⎕←⍪msg
     ⎕←''
 :EndIf
 F←##.FilesAndDirs
 :If batchFlag
 :OrIf 1 YesOrNo'Would you like to continue?'
     {}⎕SE.acre.CloseProject''('all' 1)
     #.Tatin.⎕EX¨'Client' 'Todo' 'TestCases' 'Simu'
     ⎕EX¨'Initialize_Client' 'RunTestServer' 'CreateAllRefs_Client'
     ⎕LX←'#.Tatin.Server.Run 1'

     F.MkDir'Dist'
     targetPath←'Dist/Server'
     F.RmDir targetPath
     ⎕DL 0.1
     Assert F.MkDir targetPath
     ⎕DL 0.1
     ⎕WSID←targetPath,'/Server'
     0 ⎕SAVE ⎕WSID
     F.RmDir targetPath,'/Assets/'
     ⎕DL 0.1
     F.MkDir targetPath,'/Assets/'
     ⎕DL 0.1
     Assert F.IsDir targetPath,'/Assets/'
     (success msg list)←'./Assets/'F.CopyTree targetPath,'/Assets'
     msg Assert success
     ('./server.ini.RemoveMe')F.CopyTo targetPath,'/server.ini'

     zipFilename←'Dist/Tatin-Server-',({{⍵/⍨∧\⍵∊'.',⎕D}⍵/⍨3>+\'.'=⍵}(2⊃##.Server.Version),''),'.zip'
     F.DeleteFile zipFilename
     targetPath ##.DotNetZip.ZipFolder zipFilename
     fileList←(1+≢targetPath)↓¨'recursive'F.ListFiles targetPath,'\'
     zipContent←##.DotNetZip.ListZipContents zipFilename
     'Something went wrong with the ZIP file'Assert fileList{(∧/⍺∊⍵)∧(∧/⍵∊⍺)}zipContent

     :If batchFlag
         ⎕OFF
     :Else
         ⎕←'*** Done;  note that the Tatin acre projects has been closed'
     :EndIf
 :EndIf
