 {r}←{noQLX}Make batchFlag;P;list;F;C;CFG;path;file1;file2;ts1;ts2;versionOfAPL;q;msg;rc
⍝ This function will start two APL sessions in succession in order to open the Tatin
⍝ project and then execute first `MakeClient` (which bumps the build number) and then,
⍝ in the second session, `MakeServer`, which does not bump the version number.
⍝ It also re-compiles the documentation (=creates HTML files from Markdown)
⍝ It cannot be done in one go, and it cannot be done in batch mode from the current session.
⍝ The right argument is expected to be a Boolean that tells whether this functions runs in batch
⍝ mode or not. In batch mode no messages are printed, and no interaction with a user is
⍝ attempted.
⍝ Also, Tatin is not updated in "C:\Users\<⎕AN>\Documents\Dyalog APL-64 19.0 Unicode Files\" then.
⍝ `noQLX` defaults to 0. By passing a 1 no function is called after instantiating Dyalog
⍝ for the Client and the Server; useful for debugging.
 r←⍬
 Assert(⊂batchFlag)∊0 1
 'Currently we build on Windows only, sorry'Assert'Win'≡##.APLTreeUtils2.GetOperatingSystem ⍬
 versionOfAPL←'18.0'
 :If 0=##.Client.CommTools.YesOrNo'Create Tatin with version ',versionOfAPL,' ?'
     ⎕←'Cancelled by user' ⋄ →0
 :EndIf
 F←##.FilesAndDirs ⋄ C←##.Client ⋄ CFG←##.CiderConfig ⋄ F.APLTreeUtils2←##.APLTreeUtils2
 {}F.Cd CFG.HOME
 0 0⍴{⎕←⍵}⍣(~batchFlag)⊣'   Current dir is now: ',CFG.HOME
 'Check current directory'Assert{'Tatin'≡⊃¯1↑(~⍵∊'/\')⊆⍵}1⊃1 ⎕NPARTS''
 noQLX←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'noQLX'

 0 0⍴{⎕←⍵}⍣(~batchFlag)⊣'   Compiling HTML from the markdown files (documentation)...'
 CompileDocumentation ⍬

 path←(2 ⎕NQ #'GetEnvironment' 'USERPROFILE'),'\Documents\Dyalog APL-64 19.0 Unicode Files\SessionExtensions\CiderTatin\Tatin'
 :If F.IsDir path                                           ⍝ Does a folder Tatin/  live in the version-agnostic folder?
 :AndIf F.IsFile path,'/Tatin.dyalog'                       ⍝ Does the Tatin user command script live there?
     file1←path,'/Tatin.dyalog'
     file2←##.CiderConfig.HOME,'/APLSource/Tatin.dyalog'
 :AndIf ≢/{⊃⎕NGET ⍵ 0}¨file1 file2                          ⍝ Are the two not identical ?
     (ts1 ts2)←{¯1 1 ⎕DT⊂⊃⊃('type' 3)F.Dir ⍵}¨file1 file2
     :If ts2<ts1
         q←'The Tatin user command script in',⎕UCS 13
         q,←path,⎕UCS 13
         q,←' appears to be newer than the one in the project - copy?'
         :If 1 C.YesOrNo q
             file2 ⎕NCOPY⍠('IfExists' 'Replace')⊣file1
         :EndIf
     :Else
         ⍝ We don't need to do anything because it will be copied over later on, if the user wishes so.
     :EndIf
 :EndIf
 'Create!'F.CheckPath'./Dist'
 list←⊃F.Dir'./Dist/*.zip'
 list←('.zip'∘≡¨3⊃¨⎕NPARTS¨list)/list
 list←(∨/¨{∨/¨'Tatin-Client' 'Tatin-Server' 'Tatin-Documentation'⍷¨⊂⍵}¨list)/list
 F.DeleteFile list
 P←⎕NEW ##.APLProcess
 P.Exe←GetAPLVersion versionOfAPL
 P.Args,←'load="',F.PWD,'\APLSource\Admin\Make_.aplc" '
 P.Args,←{0=≢v←2 ⎕NQ #'GetEnvironment'⍵:v ⋄ ⍵,'="',v,'"'}'DYALOGSTARTUPSE'
 P.Args,←' lx="1 #.Make_.Run 1"'
 :If 0  ⍝ Only for analyzing problems:
     P.Args,←' -exec_setup=0 '
     P.Args,←' -stop_in_setup=1 '
 :EndIf
 :If noQLX
     P.Args,←' -x'
 :EndIf
 0 0⍴{⎕←⍵}⍣(~batchFlag)⊣'   Process started for "making" the Tatin Client...'
 (rc msg)←P.Run
 msg Assert rc=0
 {⍵.HasExited:shy←0 ⋄ _←⎕DL 0.2 ⋄ ∇ ⍵}P
 'The process exited with an error code'Assert 0=P.Proc.ExitCode
 UpdateDocumentation ⍬
 P.Args←''
 P.Args,←'load="APLSource/Admin/Make_.aplc" '
 P.Args,←{0=≢v←2 ⎕NQ #'GetEnvironment'⍵:v ⋄ ⍵,'="',v,'"'}'DYALOGSTARTUPSE'
 P.Args,←' lx="0 #.Make_.Run 2"'
 P.Exe←GetAPLVersion versionOfAPL
 :If 0          ⍝ Only for analyzing problems:
     P.Args,←' -exec_setup=0'
     P.Args,←' -stop_in_setup=1 '
 :EndIf
 :If noQLX      ⍝ Only for analyzing problems:
     P.Args,←' -x'
 :EndIf
 0 0⍴{⎕←⍵}⍣(~batchFlag)⊣'   Process started for "making" the Tatin Server...'
 (rc msg)←P.Run
 msg Assert rc=0
 {⍵.HasExited:shy←0 ⋄ _←⎕DL 0.2 ⋄ ∇ ⍵}P
 'The process exited with an error code'Assert 0=P.Proc.ExitCode
 0 0⍴{⎕←⍵}⍣(~batchFlag)⊣'   The final steps, putting it all together...'
 HandleRunFunction ⍬
 UpdateTestServerAssets ⍬
 {}⎕SE.Link.Refresh'#.Tatin'
 :If ~batchFlag
 :AndIf 1 ##.Client.CommTools.YesOrNo'Would you like to install the freshly created new version?'
     ⎕←1 #.Tatin.Admin.InstallTatin 1
 :EndIf
 :If #.Tatin.Registry.Version≢⎕SE.Tatin.Version
     ⎕SE.⎕EX¨'Tatin' '_Tatin'
     ⎕←'*** New version ',({(2⊃⍵),' from ',(3⊃⍵)}##.Registry.Version),' created, but version in ⎕SE is not up-to-date anymore, please restart Dyalog'
 :Else
     #.Tatin.Registry ⎕SE.Link.Fix⊃⎕NGET(CFG.HOME,'/APLSource/Registry/Version.aplf')1  ⍝ Because building the Client changes the build number
     0 0⍴{⎕←⍵}⍣(~batchFlag)⊣'*** Done'
 :EndIf
⍝Done
