 r←{batch}InstallTatin flag;ind;installFolder;aplVersion;q;selectFlag;os;if1;if2
⍝ Installs Tatin into the appropriate folder, depending on the current OS.
⍝ The function first checks whether Tatin has been installed into either the version agnostic or
⍝ the version specific folder of the running version of Dylog, and installs into the same folder
⍝ if either is found, otherwise the user is asked which one to install into.\\
⍝ `flag` is insurance against accidental call.
⍝ `batch` defaults to 0. If it is 1 then no questions are asked.
 r←''
 :If flag
     batch←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'batch'
     aplVersion←# ⎕WG'APLVersion'
     selectFlag←0
     :Select os←##.APLTreeUtils2.GetOperatingSystem ⍬
     :Case 'Win'
         :If ~⎕NEXISTS installFolder←if1←##.Client.GetCurrentAPLsFolderInDocuments,'SessionExtensions\CiderTatin\Tatin'
         :AndIf ~⎕NEXISTS installFolder←if2←##.Client.GetUserDocumentsFolder,'Dyalog APL Files\SessionExtensions\CiderTatin\Tatin'
             selectFlag←1
         :EndIf
     :Case 'Lin'
         :If ~⎕NEXISTS installFolder←if1←(2 ⎕NQ'.' 'GetEnvironment' 'HOME'),'/dyalog.files/SessionExtensions/CiderTatin/Tatin'
         :AndIf ~⎕NEXISTS installFolder←if2←(2 ⎕NQ'.' 'GetEnvironment' 'HOME'),'/','dyalog.',('.'~⍨{⍵/⍨2>+\⍵='.'}2⊃aplVersion),'U',(¯2↑1⊃aplVersion),'.files/SessionExtensions/CiderTatin/Tatin'
             selectFlag←1
         :EndIf
     :Case 'Mac'
         :If ~⎕NEXISTS installFolder←if1←(2 ⎕NQ'.' 'GetEnvironment' 'HOME'),'/dyalog.files/SessionExtensions/CiderTatin/Tatin'
         :AndIf ~⎕NEXISTS installFolder←if2←(2 ⎕NQ'.' 'GetEnvironment' 'HOME'),'/','dyalog.',('.'~⍨{⍵/⍨2>+\⍵='.'}2⊃aplVersion),'U',(¯2↑1⊃aplVersion),'.files/SessionExtensions/CiderTatin/Tatin'
             selectFlag←1
         :EndIf
     :Else
         0 Assert⍨'OS <',os,'> is not supported'
     :EndSelect
     installFolder←⊃1 ⎕NPARTS installFolder
     :If selectFlag
         ind←'Into which folder?'##.CommTools.Select'Version specific' 'Version agnostic'
         :If 0=≢ind
             ⎕←'Cancelled by user' ⋄ →0
         :ElseIf ind=1
             installFolder←if1
         :ElseIf ind=2
             installFolder←if2
         :Else
             ∘∘∘  ⍝ Huh?!
         :EndIf
     :EndIf
     :If batch
         q←'Sure you want to install Tatin into:',(⎕UCS 13),'  ',installFolder,'/Tatin ?'
     :OrIf ##.CommTools.YesOrNo q
         :If 0=##.FilesAndDirs.⎕NC'APLTreeUtils2'
             Initialize_Client
         :EndIf
         {}##.FilesAndDirs.RmDir installFolder,'Tatin'
         installFolder ⎕NCOPY ##.CiderConfig.HOME,'/Dist/Client/Tatin'
         r←'*** Tatin installed in ',installFolder
         ⎕DL 2
         ⎕SE.SALTUtils.ResetUCMDcache ¯1
     :EndIf
 :EndIf
