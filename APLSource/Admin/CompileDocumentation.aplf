 {r}←CompileDocumentation dummy;F;targetFolder;htmlFiles;markdownFiles;markdownFile;parms;html;ns;zipFilename;myZip
⍝ Convert all markdown files in the docs/ folder to HTML and then zip the HTML files into TatinDocumentation.zip
 F←##.FilesAndDirs
 targetFolder←'./Dist/'
 htmlFiles←⊃F.Dir'./docs/*.html'
 F.DeleteFile¨htmlFiles
 markdownFiles←⊃F.Dir'./docs/*.md'
 :For markdownFile :In markdownFiles
     parms←##.MarkAPL.CreateParms
     parms.inputFilename←markdownFile
     parms.outputFilename←'html/',(2⊃⎕NPARTS markdownFile),'.html'
     parms.homeFolder←'Assets/MarkAPL'
     parms.verbose←0
     (html ns)←parms ##.MarkAPL.Markdown2HTML''
     'The markdown-to-HTML conversion run into problems'Assert 0=≢ns.report
 :EndFor
 htmlFiles←{⊃{⍺,⍵}/1↓⎕NPARTS ⍵}¨⊃F.Dir'html/*.html'
 zipFilename←targetFolder,'Tatin-Documentation-',({⍵/⍨3>+\'.'=⍵}2⊃##.Registry.Version),'.zip'
 F.DeleteFile zipFilename
 myZip←⎕NEW ##.DotNetZip(,⊂zipFilename)
 (F.PWD,'/html')∘{⍺ myZip.Add ⍵}¨htmlFiles
⍝Done
