 {r}←CompileDocumentation dummy;F;list;myZip;zipFilename;targetFolder;this;parms;html;ns
⍝ Convert all markdown files in the docs/ folder to HTML and then zip the HTML files into TatinDocumentation.zip
 F←##.FilesAndDirs
 targetFolder←'Dist/'
 list←⊃F.Dir'docs/*.html'
 F.DeleteFile¨list
 list←⊃F.Dir'docs/*.md'
 :For this :In list
     parms←##.MarkAPL.CreateParms
     parms.inputFilename←this
     parms.outputFilename←'docs/',(2⊃⎕NPARTS this),'.html'
     parms.homeFolder←'Assets/MarkAPL'
     parms.verbose←0
     (html ns)←parms ##.MarkAPL.Markdown2HTML''
     'The markdown-to-HTML conversion run into problems'Assert 0=≢ns.report
 :EndFor
 list←{⊃{⍺,⍵}/1↓⎕NPARTS ⍵}¨⊃F.Dir'docs/*.html'
 zipFilename←targetFolder,'Tatin-Documentation-',({⍵/⍨3>+\'.'=⍵}2⊃##.Registry.Version),'.zip'
 F.DeleteFile zipFilename
 myZip←⎕NEW ##.DotNetZip(,⊂zipFilename)
 (F.PWD,'/docs')∘{⍺ myZip.Add ⍵}¨list
 ⎕EX'myZip'
⍝Done
