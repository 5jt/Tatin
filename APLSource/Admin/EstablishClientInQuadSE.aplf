 {r}←{pathToUserSettings}EstablishClientInQuadSE dummy;C;Establish;ref;reg
⍝ This function is called in order to establish the Tatin Client in ⎕SE._Tatin and to establish
⍝ the API within ⎕SE.Tatin.
⍝ By default the UserSettings class is instantiated with the default path as argument.
⍝ Instead a left argument might be provided: that must be the folder that hosts the user settings.
 r←⍬
 pathToUserSettings←{0<⎕NC ⍵:⍎⍵ ⋄ ##.Client.GetUserHomeFolder ⍬}'pathToUserSettings'
 CreateAllRefs_Client
 ⎕SE.⎕EX'Tatin'
 ref←⍎'Tatin'⎕SE.⎕NS''
 Establish←{⎕SE.Tatin.⍎⍵,'←{⍺←⊢ ⋄ ⍺ ⎕SE._Tatin.Client.',⍵,' ⍵}'}
 Establish'GetDependencyTree'
 Establish'InitialisePackage'
 Establish'InstallPackage'
 Establish'ListPackages'
 Establish'ListTags'
 Establish'LoadDependencies'
 Establish'LoadPackage'
 Establish'Pack'
 Establish'PublishPackage'
 Establish'ReadPackageConfigFile'
 Establish'ListRegistries'
 Establish'ListVersions'
 Establish'HelpOnSyntax'
 Establish'GetUserHomeFolder'
 Establish'Init'
 ⎕SE.Tatin.⎕FX↑'r←Version' '⍝ Returns "name", "version" and "date"' 'r←⎕SE._Tatin.Registry.Version'  ⍝ Niladic!
 ref.DefineRegistry←##.Client.DefineRegistry
 ref.UserSettings←##.Client.UserSettings
 C←##.Client
 C.InitUserSettings pathToUserSettings
 ⎕SE.Tatin.MyUserSettings←C.MyUserSettings
 InitConga ⍬
⍝Done
