 {r}←RunTestServer y;trapFlag;testFlag;iniFile;args;bool
⍝ This function is used to run a test server started by the Tatin test cases.
⍝ `testFlag`, if 1, allows additional commands to be executed useful only for tests.
⍝ `trapFlag`, if specified and 1, overwites any INI settings and activates error trapping.
 ⎕TRAP←0 'S'
 r←⍬
 ⎕IO←1 ⋄ ⎕ML←1
 (testFlag trapFlag)←2↑y,(≢y)↓¯1 ¯1
 args←2 ⎕NQ #'GetCommandLineArgs'
 :If ∨/bool←'SERVERPATH='∘{⍺≡1 ⎕C(≢⍺)↑⍵}¨args   ⍝ On Windows, there is (must be) a command line arg SERVERPATH
     iniFile←((1+≢'serverPath')↓args⊃⍨⍸bool),'/Server/server.ini'
 :ElseIf 2=##.TestCasesServer.⎕NC'∆TEMP_SERVER_FOLDER'
     iniFile←##.TestCasesServer.∆TEMP_SERVER_FOLDER,'/Server/server.ini'
 :Else
     iniFile←##.FilesAndDirs.PWD,'/server.ini'      ⍝ Elsewere the current dir rules
 :EndIf
 r←iniFile(PrepareTestServer ##.Plodder.Run)testFlag trapFlag
 ##.Server.∆SHUTDOWN←0
⍝Done
